
Функция ПолучитьHTMLИзЗаявки(Заявка) Экспорт
	Возврат РаботаСHTML.ПолучитьHTMLИзЗаявки(Заявка);
КонецФункции

Функция ПолучитьРеквизитыЗаказчика(Заказчик) Экспорт
	СтруктураОтвета	= Новый Структура;
	СтруктураОтвета.Вставить("БизнесНаправление", Заказчик.Родитель.БизнесНаправление);
	СтруктураОтвета.Вставить("Организация", Заказчик.Организация);
	Возврат СтруктураОтвета;
КонецФункции 

Функция ИзменитьСостояниеЗарегистрированнойЗаявки(Заявка, ВидДеятельности) Экспорт
		
	Если ВидДеятельности = Справочники.ВидыДеятельности.Разработка Или ВидДеятельности = Справочники.ВидыДеятельности.ИсправлениеОшибок Тогда
		СостояниеЗаявки	= ОбщегоНазначенияСервер.ИзменитьСостояниеЗаявки(Заявка, "НаСогласовании");
	Иначе
		СостояниеЗаявки	= ОбщегоНазначенияСервер.ИзменитьСостояниеЗаявки(Заявка, "ПереданаКИсполнению");
	КонецЕсли;	
	
	Возврат СостояниеЗаявки;
	
КонецФункции

Функция КонтрольРелизовСервер(ИнформационнаяСистема, ДатаВыполнения = Неопределено, ВидДеятельности = Неопределено) Экспорт
	
	СтруктураОтвета	= Новый Структура;
	СтруктураОтвета.Вставить("КонтрольРелизов", ИнформационнаяСистема.КонтрольРелизов);
		
	Если ВидДеятельности = Справочники.ВидыДеятельности.Разработка Или ВидДеятельности = Справочники.ВидыДеятельности.ИсправлениеОшибок Тогда 
		
		Если ДатаВыполнения = Неопределено Или ДатаВыполнения = Дата(1,1,1) Тогда
			ДатаВыполнения	= ТекущаяДатаСеанса();
		КонецЕсли;	
		
		пЗапрос	= Новый Запрос;
		пЗапрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
		|	РелизыИнформационныеСистемы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Релизы.ИнформационныеСистемы КАК РелизыИнформационныеСистемы
		|ГДЕ
		|	РелизыИнформационныеСистемы.ИнформационнаяСистема = &ИнформационнаяСистема
		|	И РелизыИнформационныеСистемы.Ссылка.ДатаВыхода >= &ДатаВыхода
		|
		|УПОРЯДОЧИТЬ ПО
		|	РелизыИнформационныеСистемы.Ссылка.ДатаВыхода";
		
		пЗапрос.УстановитьПараметр("ИнформационнаяСистема", ИнформационнаяСистема);
		пЗапрос.УстановитьПараметр("ДатаВыхода", КонецМесяца(ДатаВыполнения)+10);
		
		РезультатЗапроса	= пЗапрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда 
			СтруктураОтвета.Вставить("Релиз", Справочники.Релизы.ПустаяСсылка());
		Иначе 
			Выборка	= РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			СтруктураОтвета.Вставить("Релиз", Выборка.Ссылка);		
		КонецЕсли;
		
	Иначе		
		СтруктураОтвета.Вставить("Релиз", Справочники.Релизы.ПустаяСсылка());	
	КонецЕсли;

	Возврат СтруктураОтвета;
	
КонецФункции

Функция ПроверитьИсториюСогласования(Заявка) Экспорт
	Возврат БизнесПроцессы.ОбработкаЗаявки.ПроверитьИсториюСогласования(Заявка);
КонецФункции

Процедура ВидДеятельностиПриИзменении(Объект) Экспорт	
	Если Объект.ВидДеятельности	<> Справочники.ВидыДеятельности.Разработка И Объект.ВидДеятельности	<> Справочники.ВидыДеятельности.ИсправлениеОшибок Тогда
		Объект.Релиз		= Справочники.Релизы.ПустаяСсылка();
		Объект.Тестировщик	= Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;	
КонецПроцедуры

Процедура ДобавитьВУРВСервер(Задача, Время) Экспорт
	ПередаваемыеПараметры	= Новый Массив;
	ПередаваемыеПараметры.Добавить(Задача);
	ПередаваемыеПараметры.Добавить(Время);
	ПередаваемыеПараметры.Добавить(ПараметрыСеанса.ТекущийПользователь);
	ФоновыеЗадания.Выполнить("Привилегированный.ДобавитьЗадачуВУРВ", ПередаваемыеПараметры, Новый УникальныйИдентификатор, "Добавить задачу в УРВ");
КонецПроцедуры

