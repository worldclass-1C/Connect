
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ МЕТОДЫ

&НаКлиенте
// Процедура ОбработатьРегламентныеЗадания выполняет запуск
// обработки регламентных заданий для эмуляции серверной процедуры их выполнения.
//  Подключается, как обработчик ожидания ПриОткрытии.
// 
Процедура ОбработатьРегламентныеЗадания()

	Если НужноПрекратитьОбработку(ПараметрЗапуска) Тогда
		Закрыть();
	Иначе
		ОсталосьДоНачалаОбработки = ОсталосьДоНачалаОбработки - 1;
		Если ОсталосьДоНачалаОбработки <= 0 Тогда

			ОсталосьДоНачалаОбработки = ПериодОбработки;

			СтрокаСостояния = НСтр("ru = 'Выполняется обработка, дождитесь завершения... '");
			ОбновитьОтображениеДанных();

			РегламентныеЗаданияПолныеПрава.ОбработатьРегламентныеЗадания();

		КонецЕсли;
	КонецЕсли;
	
	// Если пользователь нажал CTRL+BREAK тогда прекратим обработку
	ПодключитьОбработчикОжидания("ПрекратитьОбработкуИЗавершитьСеансВыполнить", 1, Истина);
	ОбработкаПрерыванияПользователя();
	ОтключитьОбработчикОжидания("ПрекратитьОбработкуИЗавершитьСеансВыполнить");
	
	ПодключитьОбработчикОжидания("ОбработатьРегламентныеЗадания", 1, Истина);
	СтрокаСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	                    НСтр("ru = 'До начала обработки осталось секунд: %1'"), Строка(ОсталосьДоНачалаОбработки) );
	
КонецПроцедуры // ОбработатьРегламентныеЗадания()

&НаСервере
// Функция объединяет два серверных вызова в один.
Функция НужноПрекратитьОбработку(ПараметрЗапуска)
	
	Возврат РегламентныеЗаданияПолныеПрава.РодительскийСеансЗаданИЗавершен(ПараметрЗапуска) ИЛИ
	        НЕ РегламентныеЗаданияПолныеПрава.ТекущийСеансОбрабатываетЗадания();
	
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКИ СОБЫТИЙ
//
// Источники: форма, панели, команды

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьРегламентныеЗадания();

КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПрекратитьОбработкуИЗавершитьСеансВыполнить()
	
	Закрыть();
	
КонецПроцедуры // ПрекратитьОбработкуИЗавершитьСеансВыполнить()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПериодОбработки = 3;                              // Секунды.
	ОсталосьДоНачалаОбработки = ПериодОбработки + 1;  // Секунды.
	
КонецПроцедуры

