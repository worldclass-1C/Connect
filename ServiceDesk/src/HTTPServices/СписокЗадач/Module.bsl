
Функция СписокGET(Запрос)
		
	ТабельныйНомер	= Запрос.ПараметрыURL["ТабельныйНомер"];
	Состояние		= Запрос.ПараметрыURL["Состояние"];
	
	Исполнитель	= Справочники.Пользователи.НайтиПоРеквизиту("ТабельныйНомер", ТабельныйНомер);
	
	Если Исполнитель.Пустая() Тогда		
		Ответ = Новый HTTPСервисОтвет(404);				
	Иначе                                   		
		
		Соответствие	= Новый Соответствие;
		Соответствие.Вставить("Submited", Справочники.СостоянияЗаявки.ПереданаКИсполнению);		
		Соответствие.Вставить("InProcess", Справочники.СостоянияЗаявки.ВРаботе);
		Соответствие.Вставить("OnHold", Справочники.СостоянияЗаявки.ВОжидании);
		Соответствие.Вставить("OnTest", Справочники.СостоянияЗаявки.НаПроверке);		
		Соответствие.Вставить("Completed", Справочники.СостоянияЗаявки.Выполнена);
		Соответствие.Вставить("Canceled", Справочники.СостоянияЗаявки.Отменена);
		
		//Ищем задачи по сотруднику
		пЗапрос	= Новый Запрос;
		пЗапрос.Текст	= "ВЫБРАТЬ
		             	  |	СтатусыЗаявокОстатки.Заявка.Номер КАК Номер,
		             	  |	СтатусыЗаявокОстатки.Заявка.Дата КАК Дата,
		             	  |	СтатусыЗаявокОстатки.Заявка.Заказчик КАК Заказчик,
		             	  |	СтатусыЗаявокОстатки.Заявка.ЗадачаКратко КАК Тема,
		             	  |	СтатусыЗаявокОстатки.СостояниеЗаявки КАК СостояниеЗаявки
		             	  |ИЗ
		             	  |	РегистрНакопления.СтатусыЗаявок.Остатки(
		             	  |			,
		             	  |			Исполнитель = &Исполнитель
		             	  |				И СостояниеЗаявки = &СостояниеЗаявки) КАК СтатусыЗаявокОстатки";
						  
		пЗапрос.УстановитьПараметр("Исполнитель", Исполнитель);
		
		Если Состояние = "AllStates" Тогда
			пЗапрос.Текст = СтрЗаменить(пЗапрос.Текст, "И СостояниеЗаявки = &СостояниеЗаявки","");	
		Иначе
			пЗапрос.УстановитьПараметр("СостояниеЗаявки", Соответствие[Состояние]);
		КонецЕсли;
		
		ТЗ	= пЗапрос.Выполнить().Выгрузить();
		
		ЗаписьXML	= Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку();
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписьXML.ЗаписатьНачалоЭлемента("answer");
		
		Для Каждого СтрокаТЗ Из ТЗ Цикл
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("task");
			
			ЗаписьXML.ЗаписатьАтрибут("number", Строка(СтрокаТЗ.Номер));
			ЗаписьXML.ЗаписатьАтрибут("data", Строка(СтрокаТЗ.Дата));
			ЗаписьXML.ЗаписатьАтрибут("customer", Строка(СтрокаТЗ.Заказчик));
			ЗаписьXML.ЗаписатьАтрибут("subject", Строка(СтрокаТЗ.Тема));
			ЗаписьXML.ЗаписатьАтрибут("State",Строка(СтрокаТЗ.СостояниеЗаявки));
			
			ЗаписьXML.ЗаписатьКонецЭлемента();//task
			
		КонецЦикла; 
		
		ЗаписьXML.ЗаписатьКонецЭлемента();//answer
		XMLСтрока	= ЗаписьXML.Закрыть();
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.УстановитьТелоИзСтроки(XMLСтрока);		
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции
