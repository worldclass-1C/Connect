
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаПользователя") Тогда
		
		Дата				= ТекущаяДатаСеанса();
		Заказчик			= ДанныеЗаполнения.Заказчик;		
		БизнесНаправление	= ДанныеЗаполнения.БизнесНаправление;
		Тестировщик			= ДанныеЗаполнения.Тестировщик;
		ДатаВыполнения		= ДобавитьМесяц(Дата, 1);
		
		Важность			= ДанныеЗаполнения.Важность;
		ВидДеятельности 	= ДанныеЗаполнения.ВидДеятельности;
		Основание			= ДанныеЗаполнения.Ссылка;
			
		База1С				= ДанныеЗаполнения.База1С;
		Проект				= ДанныеЗаполнения.Проект;
		ЭтапПроекта			= ДанныеЗаполнения.ЭтапПроекта;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ВложенныеФайлы.Очистить();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ДополнительныеСвойства.Свойство("СостояниеЗаявки") Тогда
		пСостояниеЗаявки = ОбщегоНазначенияСервер.ПолучитьСостояниеЗаявки(Ссылка);
		ДополнительныеСвойства.Вставить("СостояниеЗаявки", пСостояниеЗаявки.СостояниеЗаявки);
		ДополнительныеСвойства.Вставить("СостояниеЗаявкиСтарое", пСостояниеЗаявки.СостояниеЗаявкиСтарое);
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("Модифицированность") Тогда
		ДополнительныеСвойства.Вставить("Модифицированность", Ложь);
	КонецЕсли;
	
	Если ДополнительныеСвойства.СостояниеЗаявки = Справочники.СостоянияЗаявки.ВРаботе Тогда
		ДатаВыполненияФакт	= Дата(1,1,1);
	ИначеЕсли ДополнительныеСвойства.СостояниеЗаявки = Справочники.СостоянияЗаявки.Выполнена ИЛИ ДополнительныеСвойства.СостояниеЗаявки = Справочники.СостоянияЗаявки.НаПроверке Тогда
		ДатаВыполненияФакт	= ?(ДатаВыполненияФакт = Дата(1,1,1), ТекущаяДатаСеанса(), ДатаВыполненияФакт);
	КонецЕсли;
	
	Если Не Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение И Ссылка.Пустая() Тогда
		ДополнительныеСвойства.Вставить("ЗапуститьБП", Истина);
	Иначе
		ДополнительныеСвойства.Вставить("ЗапуститьБП", Ложь);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ДополнительныеСвойства.ЗапуститьБП Тогда 
		ОбщегоНазначенияСервер.ИзменитьСостояниеЗаявки(Ссылка, "Зарегистрирована");		
	КонецЕсли;
		
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение И ДополнительныеСвойства.Модифицированность Тогда 
		РаботаСБизнесПроцессами.ОбработатьСобытияДокумента("ПроведениеДокументаЗаявка", Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Ссылка.Пустая() Тогда 
		СостояниеЗаявки			= Справочники.СостоянияЗаявки.ПустаяСсылка();
		СостояниеЗаявкиСтарое   = Справочники.СостоянияЗаявки.ПустаяСсылка();
	ИначеЕсли ПометкаУдаления Тогда	
		СостояниеЗаявки			= Справочники.СостоянияЗаявки.Закрыта;
		СостояниеЗаявкиСтарое   = Справочники.СостоянияЗаявки.ПустаяСсылка();
	Иначе		
		пСостояниеЗаявки		= ОбщегоНазначенияСервер.ПолучитьСостояниеЗаявки(Ссылка);
		СостояниеЗаявки			= пСостояниеЗаявки.СостояниеЗаявки;
		СостояниеЗаявкиСтарое   = пСостояниеЗаявки.СостояниеЗаявкиСтарое;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("СостояниеЗаявки", СостояниеЗаявки);
	ДополнительныеСвойства.Вставить("СостояниеЗаявкиСтарое", СостояниеЗаявкиСтарое);
	
	Если СостояниеЗаявки.ПроверятьЗаполнениеПлановыхПоказателей Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаВыполнения");		
	КонецЕсли;
	
	Если СостояниеЗаявки = Справочники.СостоянияЗаявки.ПереданаКИсполнению Тогда
		ПроверяемыеРеквизиты.Добавить("База1С");
		ПроверяемыеРеквизиты.Добавить("ДатаВыполнения");
		ПроверяемыеРеквизиты.Добавить("БизнесНаправление");
		ПроверяемыеРеквизиты.Добавить("Исполнитель");
		ПроверяемыеРеквизиты.Добавить("Заказчик");
	КонецЕсли;
	
	Если ВидДеятельности = Справочники.ВидыДеятельности.Разработка Или ВидДеятельности = Справочники.ВидыДеятельности.ИсправлениеОшибок Тогда
		
		//Если Истина
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.ПустаяСсылка()
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.Зарегистрирована
		//	//И СостояниеЗаявки <> Справочники.СостоянияЗаявки.НаСогласовании
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.ОписаниеПроектногоРешения
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.СогласованиеПроектногоРешения1
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.СогласованиеПроектногоРешения2
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.Отменена
		//	И СостояниеЗаявки <> Справочники.СостоянияЗаявки.Закрыта
		//Тогда
		//	ПроверяемыеРеквизиты.Добавить("Тестировщик");
		//КонецЕсли;
		
		Если База1С.КонтрольРелизов Тогда
			Если СостояниеЗаявки = Справочники.СостоянияЗаявки.ПереданаКИсполнению Тогда
				ПроверяемыеРеквизиты.Добавить("Релиз");
			КонецЕсли;
		КонецЕсли;
		
		Если СостояниеЗаявки = Справочники.СостоянияЗаявки.ВРаботе Тогда
			ПроверяемыеРеквизиты.Добавить("Трудоёмкость");	
		КонецЕсли;		
				
	КонецЕсли;	
	
КонецПроцедуры













