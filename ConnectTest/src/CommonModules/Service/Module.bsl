
Function getErrorDescription(language = "", error = "", description = "") Export
	
	errorDescription	= New Structure("error, description", error, description);
	
	If description = "" Then
		If error = "noRequest" Then
			If language = "ru" Then
				errorDescription.Insert("description", "Не определен запрос");
			Else
				errorDescription.Insert("description", "no request");
			EndIf;
		ElsIf error = "userNotIdentified" Then
			If language = "ru" Then
				errorDescription.Insert("description", "Пользователь не идентифицирован");
			Else
				errorDescription.Insert("description", "User not identified");
			EndIf;
		ElsIf error = "userPasswordExpired" Then
			If language = "ru" Then
				errorDescription.Insert("description", "Пароль просрочен");
			Else
				errorDescription.Insert("description", "Password expired");
			EndIf;	
		ElsIf error = "noUserLogin" Then
			If language = "ru" Then
				errorDescription.Insert("description", "Не указан логин");
			Else
				errorDescription.Insert("description", "No user login");
			EndIf;
		ElsIf error = "noUserPassword" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан пароль");
			Else
				errorDescription.Insert("description", "No user password");
			EndIf;
		ElsIf error = "PasswordIsNotCorrect" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Неверный пароль");
			Else
				errorDescription.Insert("description", "Password is not correct");
			EndIf;
		ElsIf error = "passwordIsEmpty" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Пароль не может быть пустым");
			Else
				errorDescription.Insert("description", "Password can not be empty");
			EndIf;	
		ElsIf error = "noKpoCode" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан код сети");
			Else
				errorDescription.Insert("description", "No net code");
			EndIf;
		ElsIf error = "noAuthKey" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан код авторизации");
			Else
				errorDescription.Insert("description", "No auth-key");
			EndIf;
		ElsIf error = "noAppType" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан тип приложения");
			Else
				errorDescription.Insert("description", "No app type");
			EndIf;
		ElsIf error = "noDeviceToken" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан токен устройства");
			Else
				errorDescription.Insert("description", "No device token");
			EndIf;
		ElsIf error = "noSystemType" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан тип ОС");
			Else
				errorDescription.Insert("description", "No system type");
			EndIf;
		ElsIf error = "noSystemVersion" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указана версия ОС");
			Else
				errorDescription.Insert("description", "No system version");
			EndIf;
		ElsIf error = "noAppVersion" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указана версия приложения");
			Else
				errorDescription.Insert("description", "No app version");
			EndIf;
		ElsIf error = "noCauses" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не заполнены причины отмены");
			Else
				errorDescription.Insert("description", "No causes");
			EndIf;
		ElsIf error = "staffOnly" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Только для сотрудников");
			Else
				errorDescription.Insert("description", "staff only");
			EndIf;	
		ElsIf error = "noUrl" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан url");
			Else
				errorDescription.Insert("description", "No url");
			EndIf;
		ElsIf error = "noRoutes" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указаны каналы информирования");
			Else
				errorDescription.Insert("description", "No information routes");
			EndIf;
		ElsIf error = "noMessages" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Нет сообщений");
			Else
				errorDescription.Insert("description", "No messages");
			EndIf;
		ElsIf error = "noUserPhone" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указан телефон");
			Else
				errorDescription.Insert("description", "no user phone");
			EndIf;
		ElsIf error = "noNoteId" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Не указано уведомление");
			Else
				errorDescription.Insert("description", "no notification");
			EndIf;
		ElsIf error = "limitExceeded" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Превышен лимит сообщений");
			Else
				errorDescription.Insert("description", "Message limit exceeded");
			EndIf;	
		ElsIf error = "messageCanNotSent" Then	
			Если language = "ru" Then
				errorDescription.Insert("description", "Повторное сообщение можно отправить через 15 минут");
			Else
				errorDescription.Insert("description", "Message can be sent in 15 minutes");
			EndIf;
		EndIf;
	EndIf;
	
	Return errorDescription;
	
EndFunction

Function getRecorder(day, reportPeriod)

	begOfDay	= BegOfDay(day);

	query	= New Query();
	query.Text	= "ВЫБРАТЬ
	|	РегистраторДвижений.Ссылка КАК Ref
	|ИЗ
	|	Документ.РегистраторДвижений КАК РегистраторДвижений
	|ГДЕ
	|	РегистраторДвижений.Дата = &day
	|	И РегистраторДвижений.ПериодОтчета = &reportPeriod";
	
	query.SetParameter("day", begOfDay);
	query.SetParameter("reportPeriod", reportPeriod);
	queryResult	= query.Execute();
		
	If queryResult.IsEmpty() Then
		docObject				= Documents.РегистраторДвижений.CreateDocument();
		docObject.Date			= begOfDay;
		docObject.ПериодОтчета	= reportPeriod;		
		docObject.Write();
		Return docObject.Ref;
	Else
		selection = queryResult.Select();		
		selection.Next();
		Return selection.Ref;
	EndIf;

EndFunction 

Function canSendSms(language, phone) Export

	errorDescription	= Service.getErrorDescription();
	universalTime		= ToUniversalTime(CurrentDate());
	
	query	= New Query();
	query.Text	= "ВЫБРАТЬ
	|	РАЗНОСТЬДАТ(ИсторияСлужебныхСообщений.ДатаРегистрации, &universalTime, МИНУТА) КАК minutesPassed,
	|	ЕСТЬNULL(ИсторияСлужебныхСообщений.Количество, 0) КАК quantity,
	|	ЕСТЬNULL(ИсторияСлужебныхСообщений.ДатаРегистрации, &universalTime) КАК registrationDate
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрСведений.ИсторияСлужебныхСообщений КАК ИсторияСлужебныхСообщений
	|ГДЕ
	|	ИсторияСлужебныхСообщений.НомерТелефона = &phone
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ.minutesPassed) КАК minutesPassed,
	|	СУММА(ВТ.quantity) КАК quantity
	|ИЗ
	|	ВТ КАК ВТ
	|ГДЕ
	|	ВТ.registrationDate >= &registrationDate
	|ИМЕЮЩИЕ
	|	СУММА(ВТ.quantity) > 0";
	
	query.SetParameter("phone", phone);
	query.SetParameter("registrationDate", AddMonth(universalTime, - 1));
	query.SetParameter("universalTime", universalTime);
	
	queryResult	= query.Execute();
	
	If Not queryResult.IsEmpty() Then		
		selection = queryResult.Select();
		selection.Next();		
		If selection.quantity > 3 Then
			errorDescription	= Service.getErrorDescription(language, "limitExceeded");
		ElsIf selection.minutesPassed < 15 Then
			errorDescription	= Service.getErrorDescription(language, "messageCanNotSent");
		EndIf;				
	EndIf;
	
	Return errorDescription;

EndFunction 

Procedure FixSandingMessage(phone) Export
	
	universalTime		= ToUniversalTime(CurrentDate());
	
	record	= InformationRegisters.ИсторияСлужебныхСообщений.CreateRecordManager();
	record.НомерТелефона	= phone;
	record.Read();
	If record.Selected() Then		
		If record.ДатаРегистрации < AddMonth(universalTime, - 1) Then
			record.Количество = 1;
		Else
			record.Количество = record.Количество + 1;
		EndIf;
		record.ДатаРегистрации	= universalTime;
	Else
		record.НомерТелефона	= phone;
		record.ДатаРегистрации	= universalTime;
		record.Количество		= 1;
	EndIf;
	
	record.Write();
	
EndProcedure

Процедура ЗафиксироватьЗапросВИсторииВФоне(Параметры) Экспорт //Длительность, ИмяЗапроса, Запрос, Ответ, Ошибка, Токен, Пользователь)Экспорт
	ПередаваемыеПараметры	= Новый Массив;
	ПередаваемыеПараметры.Добавить(Параметры);
	ФоновыеЗадания.Выполнить("Service.ЗафиксироватьЗапросВИстории", ПередаваемыеПараметры, Новый УникальныйИдентификатор);
КонецПроцедуры
	
Процедура ЗафиксироватьЗапросВИстории(Параметры) Экспорт
		
	Запись	= Справочники.ИсторииЗапросов.СоздатьЭлемент();
	Запись.Период				= УниверсальноеВремя(ТекущаяДата());
	Запись.Токен				= Параметры.Токен;	
	Запись.ИмяЗапроса			= Параметры.ИмяЗапроса;
	Запись.Длительность			= Параметры.Длительность;
	Запись.Ошибка				= Параметры.Ошибка;	
	
	ТелоЗапроса					= """Headers"":" + Символы.ПС + Параметры.Заголовки + Символы.ПС + """Body"":" + Символы.ПС + Параметры.ТелоЗапроса;
	Если Параметры.СжиматьЛоги Тогда
		Запись.Запрос			= Новый ХранилищеЗначения(Base64Значение(СериализаторXDTO.XMLСтрока(Новый ХранилищеЗначения(ТелоЗапроса, Новый СжатиеДанных(9)))));	
	Else                   	
		Запись.ЗапросТело		= ТелоЗапроса;
	EndIf;
	
	Если Не Параметры.НеСохранятьОтветВЛогах Или Параметры.Ошибка Тогда
		Если Параметры.СжиматьЛоги Тогда
			Запись.Ответ		= Новый ХранилищеЗначения(Base64Значение(СериализаторXDTO.XMLСтрока(Новый ХранилищеЗначения(Параметры.ТелоОтвета, Новый СжатиеДанных(9)))));
		Else
			Запись.ОтветТело	= Параметры.ТелоОтвета;
		EndIf;
	EndIf;
	
	Запись.Записать();		
		
КонецПроцедуры

Процедура РассчитатьПоказатели() Экспорт
	
	//Расчет показателей по дням
	Дни				= Новый Массив;
	ПредыдущийДень	= НачалоДня(УниверсальноеВремя(ТекущаяДата()) - 86400);
	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	             	  |	РегистраторДвижений.Дата КАК ДеньРасчетаПоказателей
	             	  |ИЗ
	             	  |	Документ.РегистраторДвижений КАК РегистраторДвижений
	             	  |ГДЕ
	             	  |	РегистраторДвижений.ПериодОтчета = ЗНАЧЕНИЕ(Перечисление.ПериодыОтчета.День)
	             	  |
	             	  |УПОРЯДОЧИТЬ ПО
	             	  |	ДеньРасчетаПоказателей УБЫВ";
	
	РезультатЗапроса	= пЗапрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Дни.Добавить(ПредыдущийДень);
	Else
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ДеньРасчетаПоказателей	= Выборка.ДеньРасчетаПоказателей;
		Пока ДеньРасчетаПоказателей < ПредыдущийДень Цикл
			ДеньРасчетаПоказателей	= ДеньРасчетаПоказателей + 86400; 
			Дни.Добавить(ДеньРасчетаПоказателей);
		КонецЦикла;		
	EndIf;	
	
	Service.РассчитатьПоказателиПоДням(Дни);	
	
	//Расчет показателей по месяцам
	Месяцы			= Новый Массив;
	ТекущийМесяц	= НачалоМесяца(УниверсальноеВремя(ТекущаяДата()));	
	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	             	  |	РегистраторДвижений.Дата КАК МесяцРасчетаПоказателей,
	             	  |	РегистраторДвижений.Ссылка КАК Ссылка
	             	  |ИЗ
	             	  |	Документ.РегистраторДвижений КАК РегистраторДвижений
	             	  |ГДЕ
	             	  |	РегистраторДвижений.ПериодОтчета = ЗНАЧЕНИЕ(Перечисление.ПериодыОтчета.Месяц)
	             	  |
	             	  |УПОРЯДОЧИТЬ ПО
	             	  |	МесяцРасчетаПоказателей УБЫВ";
	
	РезультатЗапроса = пЗапрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда		
		Месяцы.Добавить(ТекущийМесяц);
	Else
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		МесяцРасчетаПоказателей	= Выборка.МесяцРасчетаПоказателей;		
		Месяцы.Добавить(МесяцРасчетаПоказателей);		
		Пока МесяцРасчетаПоказателей < ТекущийМесяц Цикл
			МесяцРасчетаПоказателей	= ДобавитьМесяц(МесяцРасчетаПоказателей, 1);
			Месяцы.Добавить(МесяцРасчетаПоказателей);
		КонецЦикла;		
	EndIf;	
	
	Service.РассчитатьПоказателиПоМесяцам(Месяцы);
	
КонецПроцедуры	
	
Процедура РассчитатьПоказателиПоДням(Дни) Экспорт
	Для Каждого День Из Дни Цикл
		РассчитатьПоказателиЗаДень(День);	
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьПоказателиЗаДень(День) Экспорт
	
	Набор	= РегистрыНакопления.ПоказателиПользователей.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(GetRecorder(День, Перечисления.ПериодыОтчета.День));
	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ
	             	  |	ИсторияЗапросов.Период КАК Период,
	             	  |	ИсторияЗапросов.ИмяЗапроса КАК ИмяЗапроса,
	             	  |	ИсторияЗапросов.Токен КАК Токен,
	             	  |	ИсторияЗапросов.Токен.Пользователь КАК Пользователь,
	             	  |	ИсторияЗапросов.Токен.Холдинг КАК Холдинг,
	             	  |	АналитикиПриложений.Ссылка КАК АналитикаПриложений
	             	  |ПОМЕСТИТЬ ВТ_ИсторияЗапросов
	             	  |ИЗ
	             	  |	Справочник.ИсторииЗапросов КАК ИсторияЗапросов
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АналитикиПриложений КАК АналитикиПриложений
	             	  |		ПО ИсторияЗапросов.Токен.ВидПриложения = АналитикиПриложений.ВидПриложения
	             	  |			И ИсторияЗапросов.Токен.ОперационнаяСистема = АналитикиПриложений.ОперационнаяСистема
	             	  |ГДЕ
	             	  |	ИсторияЗапросов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	             	  |	И НЕ АналитикиПриложений.Ссылка ЕСТЬ NULL
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	ВТ_ИсторияЗапросов.Период КАК Период,
	             	  |	РАЗНОСТЬДАТ(ВТ_ИсторияЗапросов.Период, МИНИМУМ(ЕСТЬNULL(ВТ_ИсторияЗапросов1.Период, &Завтра)), МИНУТА) КАК Дельта,
	             	  |	ВТ_ИсторияЗапросов.ИмяЗапроса КАК ИмяЗапроса,
	             	  |	ВТ_ИсторияЗапросов.Токен КАК Токен,
	             	  |	ВТ_ИсторияЗапросов.Пользователь КАК Пользователь,
	             	  |	ВТ_ИсторияЗапросов.Холдинг КАК Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений КАК АналитикаПриложений
	             	  |ПОМЕСТИТЬ ВТ_Сеансы
	             	  |ИЗ
	             	  |	ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов1
	             	  |		ПО ВТ_ИсторияЗапросов.Токен = ВТ_ИсторияЗапросов1.Токен
	             	  |			И ВТ_ИсторияЗапросов.Период < ВТ_ИсторияЗапросов1.Период
	             	  |ГДЕ
	             	  |	ВТ_ИсторияЗапросов.Токен <> ЗНАЧЕНИЕ(Справочник.Токены.ПустаяСсылка)
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_ИсторияЗапросов.Период,
	             	  |	ВТ_ИсторияЗапросов.ИмяЗапроса,
	             	  |	ВТ_ИсторияЗапросов.Токен,
	             	  |	ВТ_ИсторияЗапросов.Пользователь,
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала КАК Период,
	             	  |	ВТ_Сеансы.Холдинг КАК Холдинг,
	             	  |	ВТ_Сеансы.АналитикаПриложений КАК АналитикаПриложений,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.Сеансов) КАК Показатель,
	             	  |	&ПериодОтчета КАК ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Сеансы.Токен) КАК Количество
	             	  |ИЗ
	             	  |	ВТ_Сеансы КАК ВТ_Сеансы
	             	  |ГДЕ
	             	  |	ВТ_Сеансы.Дельта > 30
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_Сеансы.Холдинг,
	             	  |	ВТ_Сеансы.АналитикаПриложений
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.Регистраций),
	             	  |	&ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ИсторияЗапросов.Пользователь)
	             	  |ИЗ
	             	  |	ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов
	             	  |ГДЕ
	             	  |	ВТ_ИсторияЗапросов.Пользователь.ДатаРегистрации МЕЖДУ &ДатаНачала И &ДатаОкончания
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.АктивныхПользователей),
	             	  |	&ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ИсторияЗапросов.Пользователь)
	             	  |ИЗ
	             	  |	ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.ЗаписейНаСобытие),
	             	  |	&ПериодОтчета,
	             	  |	СУММА(ВЫБОР
	             	  |			КОГДА ВТ_ИсторияЗапросов.ИмяЗапроса = ""employeeAddChangeBooking""
	             	  |				ТОГДА 1
	             	  |			Else 0
	             	  |		КОНЕЦ)
	             	  |ИЗ
	             	  |	ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений
	             	  |
	             	  |ИМЕЮЩИЕ
	             	  |	СУММА(ВЫБОР
	             	  |			КОГДА ВТ_ИсторияЗапросов.ИмяЗапроса = ""employeeAddChangeBooking""
	             	  |				ТОГДА 1
	             	  |			Else 0
	             	  |		КОНЕЦ) > 0
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	Токены.Холдинг,
	             	  |	АналитикиПриложений.Ссылка,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.Пользователей),
	             	  |	&ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Токены.Пользователь)
	             	  |ИЗ
	             	  |	Справочник.Токены КАК Токены
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АналитикиПриложений КАК АналитикиПриложений
	             	  |		ПО Токены.ВидПриложения = АналитикиПриложений.ВидПриложения
	             	  |			И Токены.ОперационнаяСистема = АналитикиПриложений.ОперационнаяСистема
	             	  |ГДЕ
	             	  |	НЕ АналитикиПриложений.Ссылка ЕСТЬ NULL
	             	  |	И Токены.ДатаБлокировки = ДАТАВРЕМЯ(1, 1, 1)
	             	  |	И Токены.ДатаСоздания <= &ДатаОкончания
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	Токены.Холдинг,
	             	  |	АналитикиПриложений.Ссылка";
	
	пЗапрос.УстановитьПараметр("ДатаНачала", НачалоДня(День));
	пЗапрос.УстановитьПараметр("ДатаОкончания", КонецДня(День));
	пЗапрос.УстановитьПараметр("Завтра", КонецДня(День) + 86400);
	пЗапрос.УстановитьПараметр("ПериодОтчета", Перечисления.ПериодыОтчета.День);
	Набор.Загрузить(пЗапрос.Выполнить().Выгрузить());
	Набор.Записать();
		
КонецПроцедуры

Процедура РассчитатьПоказателиПоМесяцам(Месяцы) Экспорт
	Для Каждого Месяц Из Месяцы Цикл
		РассчитатьПоказателиЗаМесяц(Месяц);	
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьПоказателиЗаМесяц(Месяц) Экспорт
	
	Набор	= РегистрыНакопления.ПоказателиПользователей.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(GetRecorder(Месяц, Перечисления.ПериодыОтчета.Месяц));
	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ
	             	  |	ИсторияЗапросов.Период КАК Период,
	             	  |	ИсторияЗапросов.ИмяЗапроса КАК ИмяЗапроса,
	             	  |	ИсторияЗапросов.Токен КАК Токен,
	             	  |	ИсторияЗапросов.Токен.Пользователь КАК Пользователь,
	             	  |	ИсторияЗапросов.Токен.Холдинг КАК Холдинг,
	             	  |	АналитикиПриложений.Ссылка КАК АналитикаПриложений
	             	  |ПОМЕСТИТЬ ВТ_ИсторияЗапросов
	             	  |ИЗ
	             	  |	Справочник.ИсторииЗапросов КАК ИсторияЗапросов
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АналитикиПриложений КАК АналитикиПриложений
	             	  |		ПО ИсторияЗапросов.Токен.ВидПриложения = АналитикиПриложений.ВидПриложения
	             	  |			И ИсторияЗапросов.Токен.ОперационнаяСистема = АналитикиПриложений.ОперационнаяСистема
	             	  |ГДЕ
	             	  |	ИсторияЗапросов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	             	  |	И НЕ АналитикиПриложений.Ссылка ЕСТЬ NULL
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала КАК Период,
	             	  |	ВТ_ИсторияЗапросов.Холдинг КАК Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений КАК АналитикаПриложений,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.АктивныхПользователей) КАК Показатель,
	             	  |	&ПериодОтчета КАК ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ИсторияЗапросов.Пользователь) КАК Количество
	             	  |ИЗ
	             	  |	ВТ_ИсторияЗапросов КАК ВТ_ИсторияЗапросов
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ВТ_ИсторияЗапросов.Холдинг,
	             	  |	ВТ_ИсторияЗапросов.АналитикаПриложений
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	Токены.Холдинг,
	             	  |	АналитикиПриложений.Ссылка,
	             	  |	ЗНАЧЕНИЕ(Перечисление.Показатели.Пользователей),
	             	  |	&ПериодОтчета,
	             	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Токены.Пользователь)
	             	  |ИЗ
	             	  |	Справочник.Токены КАК Токены
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АналитикиПриложений КАК АналитикиПриложений
	             	  |		ПО Токены.ВидПриложения = АналитикиПриложений.ВидПриложения
	             	  |			И Токены.ОперационнаяСистема = АналитикиПриложений.ОперационнаяСистема
	             	  |ГДЕ
	             	  |	НЕ АналитикиПриложений.Ссылка ЕСТЬ NULL
	             	  |	И Токены.ДатаБлокировки = ДАТАВРЕМЯ(1, 1, 1)
	             	  |	И Токены.ДатаСоздания <= &ДатаОкончания
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	Токены.Холдинг,
	             	  |	АналитикиПриложений.Ссылка
	             	  |
	             	  |ОБЪЕДИНИТЬ ВСЕ
	             	  |
	             	  |ВЫБРАТЬ
	             	  |	&ДатаНачала,
	             	  |	ПоказателиПользователейОбороты.Холдинг,
	             	  |	ПоказателиПользователейОбороты.АналитикаПриложений,
	             	  |	ПоказателиПользователейОбороты.Показатель,
	             	  |	&ПериодОтчета,
	             	  |	ПоказателиПользователейОбороты.КоличествоОборот
	             	  |ИЗ
	             	  |	РегистрНакопления.ПоказателиПользователей.Обороты(
	             	  |			&ДатаНачала,
	             	  |			&ДатаОкончания,
	             	  |			,
	             	  |			ПериодОтчета = &ПериодОтчетаДень
	             	  |				И Показатель <> ЗНАЧЕНИЕ(Перечисление.Показатели.АктивныхПользователей)
	             	  |				И Показатель <> ЗНАЧЕНИЕ(Перечисление.Показатели.Пользователей)) КАК ПоказателиПользователейОбороты";
	
	пЗапрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Месяц));
	пЗапрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Месяц));	
	пЗапрос.УстановитьПараметр("ПериодОтчета", Перечисления.ПериодыОтчета.Месяц);
	пЗапрос.УстановитьПараметр("ПериодОтчетаДень", Перечисления.ПериодыОтчета.День);
	Набор.Загрузить(пЗапрос.Выполнить().Выгрузить());
	Набор.Записать();
		
КонецПроцедуры

Процедура ОповеститьИсточникИнформации() Экспорт
	
	Заголовки	= Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
		
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 100
	             	  |	ПользователиИзменения.Ссылка КАК Пользователь,
	             	  |	Токены.ВидПриложения КАК ВидПриложения,
	             	  |	МИНИМУМ(Токены.ДатаБлокировки) КАК ДатаБлокировки,
	             	  |	Токены.Холдинг КАК Холдинг
	             	  |ИЗ
	             	  |	Справочник.Пользователи.Изменения КАК ПользователиИзменения
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Токены КАК Токены
	             	  |		ПО ПользователиИзменения.Ссылка = Токены.Пользователь
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	ПользователиИзменения.Ссылка,
	             	  |	Токены.Сеть,
	             	  |	Токены.ВидПриложения,
	             	  |	Токены.Холдинг";
	
	Выборка	= пЗапрос.Выполнить().Выбрать();
	
	Узел	= ОбщегоНазначенияПовторноеИспользование.УзелРегистрацияПользователя(Перечисления.ТипыРегистрации.Регистрация);
	
	Пока Выборка.Следующий() Цикл	
		СтруктураЗапроса = HTTP.GetRequestStructure(?(Выборка.ДатаБлокировки = Дата(1,1,1), "registerAccount", "unregisterAccount"), Выборка.Холдинг);
		Если СтруктураЗапроса.Количество() > 0 Тогда			
			СтруктураHTTPЗапроса	= Новый Структура;
			СтруктураHTTPЗапроса.Вставить("userId", XMLСтрока(Выборка.Пользователь));
			СтруктураHTTPЗапроса.Вставить("language", "en");
			СтруктураHTTPЗапроса.Вставить("appType", XMLСтрока(Выборка.ВидПриложения));			
			HTTPСоединение	= Новый HTTPСоединение(СтруктураЗапроса.Сервер,, СтруктураЗапроса.УчетнаяЗапись, СтруктураЗапроса.Пароль,, СтруктураЗапроса.Таймаут, ?(СтруктураЗапроса.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), СтруктураЗапроса.ИспользоватьАутентификациюОС);
			ЗапросHTTP = Новый HTTPЗапрос(СтруктураЗапроса.URL + СтруктураЗапроса.Приемник, Заголовки);
			ЗапросHTTP.УстановитьТелоИзСтроки(HTTP.GetJSONFromStructure(СтруктураHTTPЗапроса));			
			ОтветHTTP	= HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
			Если ОтветHTTP.КодСостояния = 200 Тогда
				ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Выборка.Пользователь);
			EndIf;
		Else
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Выборка.Пользователь);
		EndIf;	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьАктуальностьТокенов() Экспорт
	
	//Проверка актуальности токенов для ОС Android	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст	= "ВЫБРАТЬ 
	             	  |	Токены.Ссылка КАК Токен,
	             	  |	РАЗНОСТЬДАТ(ЕСТЬNULL(ЗарегистрированныеУстройства.ДатаЗаписи, ДАТАВРЕМЯ(1, 1, 1)), &ТекущаяДата, ДЕНЬ) КАК АктуальностьЗаписи,
	             	  |	ЕСТЬNULL(ЗарегистрированныеУстройства.ТокенУстройства, """") КАК ТокенУстройства,
	             	  |	""GCM"" КАК ТипПодписчика,
	             	  |	Токены.Сеть КАК Сеть,
	             	  |	Токены.ВидПриложения КАК ВидПриложения,
	             	  |	Токены.ОперационнаяСистема КАК ОперационнаяСистема
	             	  |ПОМЕСТИТЬ ВТ
	             	  |ИЗ
	             	  |	Справочник.Токены КАК Токены
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗарегистрированныеУстройства КАК ЗарегистрированныеУстройства
	             	  |		ПО (ЗарегистрированныеУстройства.Токен = Токены.Ссылка)
	             	  |ГДЕ
	             	  |	Токены.ДатаБлокировки = ДАТАВРЕМЯ(1, 1, 1)
	             	  |	И Токены.ОперационнаяСистема = ЗНАЧЕНИЕ(Перечисление.ОперационныеСистемы.Android)
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ ПЕРВЫЕ 100
	             	  |	ЕСТЬNULL(СертификатПриложенияДляСети.Сертификат, СертификатПриложенияОбщий.Сертификат) КАК Сертификат,
	             	  |	ВТ.Токен КАК Токен,
	             	  |	ВТ.ТокенУстройства КАК ТокенУстройства,
	             	  |	ВТ.ТипПодписчика КАК ТипПодписчика,
	             	  |	ВТ.ОперационнаяСистема КАК ОперационнаяСистема
	             	  |ИЗ
	             	  |	ВТ КАК ВТ
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыПриложений КАК СертификатПриложенияДляСети
	             	  |		ПО ВТ.Сеть = СертификатПриложенияДляСети.Сеть
	             	  |			И ВТ.ВидПриложения = СертификатПриложенияДляСети.ВидПриложения
	             	  |			И ВТ.ОперационнаяСистема = СертификатПриложенияДляСети.ОперационнаяСистема
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыПриложений КАК СертификатПриложенияОбщий
	             	  |		ПО (СертификатПриложенияОбщий.Сеть = ЗНАЧЕНИЕ(Справочник.Сети.ПустаяСсылка))
	             	  |			И ВТ.ВидПриложения = СертификатПриложенияОбщий.ВидПриложения
	             	  |			И ВТ.ОперационнаяСистема = СертификатПриложенияОбщий.ОперационнаяСистема
	             	  |ГДЕ
	             	  |	ВТ.АктуальностьЗаписи > 7";
	
	пЗапрос.УстановитьПараметр("ТекущаяДата", УниверсальноеВремя(ТекущаяДата()));
	
	РезультатЗапроса	= пЗапрос.Выполнить();
	ТаблицаПоиска		= РезультатЗапроса.Выгрузить();
	Выборка				= РезультатЗапроса.Выбрать();	
	
	Уведомление	= Неопределено;
	Сч			= 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТокенУстройства = "" Тогда
			Справочники.Токены.ЗаблокироватьТокенПользователя(Выборка.Токен);			
		ElsIf Выборка.ТокенУстройства <> "" Тогда
			Если Уведомление = Неопределено Тогда
				Уведомление	= Новый ДоставляемоеУведомление;	
			EndIf;						
			Уведомление.Получатели.Добавить(РаботаССообщениями.ПолучательPush(Выборка.ТокенУстройства, Выборка.ТипПодписчика));			
			Сч	= Сч + 1;
		EndIf;
		
		Если Сч = 10 Тогда 
			Сч = 0;
			Уведомление.Данные				= РаботаССообщениями.ДанныеPush("registerDevice");
			Уведомление.Заголовок			= "";
			Уведомление.Текст				= "registerDevice";
					
			ИсключенныеПолучатели	= Новый Массив;			
			ОтправкаДоставляемыхУведомлений.Отправить(Уведомление, ОбщегоНазначенияПовторноеИспользование.ДанныеАутентификации(Выборка.ОперационнаяСистема, Выборка.Сертификат), ИсключенныеПолучатели);
						
			Если ИсключенныеПолучатели.Количество() > 0 Тогда				
				Для Каждого ИсключаемыйПолучатель Из ИсключенныеПолучатели Цикл
					НайденаяСтрока	= ТаблицаПоиска.Найти(ИсключаемыйПолучатель, "ТокенУстройства");					
					Если НайденаяСтрока <> Неопределено Тогда						
						Справочники.Токены.ЗаблокироватьТокенПользователя(НайденаяСтрока.Токен);
						ТаблицаПоиска.Удалить(НайденаяСтрока);
					EndIf;					
				КонецЦикла;				
			EndIf;			
			Уведомление	= Неопределено;			
		EndIf;
		
	КонецЦикла;			
			
	Если Уведомление <> Неопределено Тогда
		Уведомление.Данные				= РаботаССообщениями.ДанныеPush("registerDevice");
		Уведомление.Заголовок			= "";
		Уведомление.Текст				= "registerDevice";
		//Уведомление.ЗвуковоеОповещение	= ЗвуковоеОповещение.ПоУмолчанию;
		
		ИсключенныеПолучатели	= Новый Массив;			
		ОтправкаДоставляемыхУведомлений.Отправить(Уведомление, ОбщегоНазначенияПовторноеИспользование.ДанныеАутентификации(Выборка.ОперационнаяСистема, Выборка.Сертификат), ИсключенныеПолучатели);
		
		Если ИсключенныеПолучатели.Количество() > 0 Тогда				
			Для Каждого ИсключаемыйПолучатель Из ИсключенныеПолучатели Цикл
				НайденаяСтрока	= ТаблицаПоиска.Найти(ИсключаемыйПолучатель, "ТокенУстройства");					
				Если НайденаяСтрока <> Неопределено Тогда					
					Справочники.Токены.ЗаблокироватьТокенПользователя(НайденаяСтрока.Токен);
					ТаблицаПоиска.Удалить(НайденаяСтрока);
				EndIf;					
			КонецЦикла;				
		EndIf;			
		Уведомление	= Неопределено;
	EndIf;
	
	
	//Проверка актуальности токенов для ОС iOS	
	
	
	
	//Блокируем токены в МП тренера по уволенным сотрудникам	
	пЗапрос	= Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
	                |	Токены.Ссылка КАК Токен
	                |ИЗ
	                |	Справочник.Токены КАК Токены
	                |ГДЕ
	                |	Токены.ВидПриложения = ЗНАЧЕНИЕ(Перечисление.ВидыПриложений.Employee)
	                |	И Токены.ДатаБлокировки = ДАТАВРЕМЯ(1, 1, 1)
	                |	И Токены.Пользователь.ТипПользователя <> ""employee""";
	
	Выборка	= пЗапрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.Токены.ЗаблокироватьТокенПользователя(Выборка.Токен);
	КонецЦикла;		
	
КонецПроцедуры

Function RunBackground(selection, headers, body, link,
		RequestParametersFromURL) Export
	Parameters = New Array();
	Parameters.Add(selection);
	Parameters.Add(headers);
	Parameters.Add(body);
	Parameters.Add(link);
	Parameters.Add(RequestParametersFromURL);
	Return BackgroundJobs.Execute("HTTP.RunRequestInAccountingSystem", Parameters, New UUID);
EndFunction

Function СтруктураАтрибутовВнешнегоЗапроса(ИмяЗапроса) Export

	ТаблицаАтрибутов = New ТаблицаЗначений;
	ТаблицаАтрибутов.Колонки.Добавить("Ключ");
	ТаблицаАтрибутов.Колонки.Добавить("Значение");
	ТаблицаАтрибутов.Колонки.Добавить("Тип");

	ТаблицаАтрибутовДляНовогоЭлемента = New ТаблицаЗначений;
	ТаблицаАтрибутовДляНовогоЭлемента.Колонки.Добавить("Ключ");
	ТаблицаАтрибутовДляНовогоЭлемента.Колонки.Добавить("Значение");
	ТаблицаАтрибутовДляНовогоЭлемента.Колонки.Добавить("Тип");

	СтруктураМД = New Структура;
	ИмяОбъектаМетаданных = "";

	Если ИмяЗапроса = "users" Тогда

		ИмяОбъектаМетаданных = "Пользователи";

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Email", "email", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ДатаРождения", "birthdayDate", "Дата");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Фамилия", "lastName", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Имя", "firstName", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Отчество", "secondName", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "КодПользователя", "cid", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "НомерТелефона", "phoneNumber", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Пол", "gender", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ТипПользователя", "userType", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Штрихкод", "barcode", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "НеУчаствоватьВРассылкеEmail", "noSubscriptionEmail", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "НеУчаствоватьВРассылкеСообщений", "noSubscriptionSms", "Булево");

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовДляНовогоЭлемента, "Логин", "cid", "Строка");
		//ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовДляНовогоЭлемента, "Пароль", "password", "Строка");

	ElsIf ИмяЗапроса = "gyms" Тогда

		ИмяОбъектаМетаданных = "Клубы";

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Наименование", "name", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Адрес", "gymAddress", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Сегмент", "division", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Широта", "latitude", "Число");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Долгота", "longitude", "Число");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Тип", "type", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ГрафикРаботыОтделов", "departments", "ТабличнаяЧасть");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Город", "city", "Ссылка");

		ТаблицаАтрибутовТЧГрафикРаботыОтделов = New ТаблицаЗначений;
		ТаблицаАтрибутовТЧГрафикРаботыОтделов.Колонки.Добавить("Ключ");
		ТаблицаАтрибутовТЧГрафикРаботыОтделов.Колонки.Добавить("Значение");
		ТаблицаАтрибутовТЧГрафикРаботыОтделов.Колонки.Добавить("Тип");

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧГрафикРаботыОтделов, "Отдел", "name", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧГрафикРаботыОтделов, "Телефон", "phone", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧГрафикРаботыОтделов, "ГрафикРаботыВБудни", "weekdaysTime", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧГрафикРаботыОтделов, "ГрафикРаботыВВыходные", "holidaysTime", "Строка");

		СтруктураГород = New Структура;
		СтруктураГород.Вставить("Города", "id");

		СтруктураМД.Вставить("ГрафикРаботыОтделов", ТаблицаАтрибутовТЧГрафикРаботыОтделов);
		СтруктураМД.Вставить("Город", СтруктураГород);

	ElsIf ИмяЗапроса = "cities" Тогда

		ИмяОбъектаМетаданных = "Города";
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Наименование", "name", "Строка");

	ElsIf ИмяЗапроса = "cancelcauses" Тогда

		ИмяОбъектаМетаданных = "ПричиныОтменыЗаписи";
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Наименование", "name", "Строка");

	ElsIf ИмяЗапроса = "request" Тогда

		ИмяОбъектаМетаданных = "СоответствиеЗапросовИсточникамИнформации";

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "Код", "code", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ВыполнятьВФоне", "background", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "НеСохранятьОтветВЛогах", "notSaveLogs", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "СжиматьЛоги", "compressLogs", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ТолькоДляСотрудников", "staffOnly", "Булево");

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, "ИсточникиИнформации", "informationSources", "ТабличнаяЧасть");

		ТаблицаАтрибутовТЧИсточникиИнформации = New ТаблицаЗначений;
		ТаблицаАтрибутовТЧИсточникиИнформации.Колонки.Добавить("Ключ");
		ТаблицаАтрибутовТЧИсточникиИнформации.Колонки.Добавить("Значение");
		ТаблицаАтрибутовТЧИсточникиИнформации.Колонки.Добавить("Тип");

		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "Атрибут", "atribute", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "ВыполнятьВФоне", "background", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "НеСохранятьОтветВЛогах", "notSaveLogs", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "СжиматьЛоги", "compressLogs", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "ТолькоДляСотрудников", "staffOnly", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "НеИспользовать", "notUse", "Булево");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "ЗапросИсточник", "requestSource", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "ЗапросПриемник", "requestReceiver", "Строка");
		ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутовТЧИсточникиИнформации, "ИсточникИнформации", "informationSource", "Ссылка");

		СтруктураИсточникИнформации = New Структура;
		СтруктураИсточникИнформации.Вставить("ИсточникиИнформации", "uid");

		СтруктураМД.Вставить("ИсточникиИнформации", ТаблицаАтрибутовТЧИсточникиИнформации);
		СтруктураМД.Вставить("ИсточникИнформации", СтруктураИсточникИнформации);

	EndIf;

	Return New Структура("ИмяОбъектаМетаданных, ТаблицаАтрибутов, ТаблицаАтрибутовДляНовогоЭлемента, СтруктураМД", ИмяОбъектаМетаданных, ТаблицаАтрибутов, ТаблицаАтрибутовДляНовогоЭлемента, СтруктураМД);

EndFunction

Procedure ДобавитьСтрокуВТаблицуАтрибутов(ТаблицаАтрибутов, Ключ, Значение,
		Тип)
	НоваяСтрока = ТаблицаАтрибутов.Добавить();
	НоваяСтрока.Ключ = Ключ;
	НоваяСтрока.Значение = Значение;
	НоваяСтрока.Тип = Тип;
EndProcedure

