//SC-067295
Функция ОтправитьСМС(Параметры, Ответ) Экспорт
	
	HTTPСоединение = Новый HTTPСоединение(Параметры.Сервер,Параметры.Порт,,,,Параметры.Таймаут,?(Параметры.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), Параметры.ИспользоватьАутентификациюОС);

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	ЗапросHTTP = Новый HTTPЗапрос("/modules/send_sms.php", Заголовки);
	СтрокаПараметров = "username=" + Параметры.Пользователь + 
						"&password=" + Параметры.Пароль +
						"&to=" + Параметры.НомерТелефона +
						"&from=" + Параметры.ИмяОтправителя +
						"&coding=2" +
						"&text=" + Параметры.Текст;
	
	ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаПараметров,,ИспользованиеByteOrderMark.НеИспользовать);
	ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
	ТелоОтвета = СокрЛП(ОтветHTTP.ПолучитьТелоКакСтроку());
	
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(ТелоОтвета);
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументHTML = ПостроительDOM.Прочитать(ЧтениеHTML);
	
	Узлы = ДокументHTML.ПолучитьЭлементыПоИмени("body")[0].ДочерниеУзлы;
	
	СтрукутраРасшифровкаОтвета = РасшифроватьУзлы(Узлы);

	Если СтрукутраРасшифровкаОтвета.Свойство("Error") Тогда 
		РасшифровкаОшибки = ПолучитьРасшифровкуОшибки(СтрукутраРасшифровкаОтвета.Error);
		Ответ.Вставить("Ошибка", РасшифровкаОшибки);
	ИначеЕсли  СтрукутраРасшифровкаОтвета.Свойство("Success") Тогда
		Ответ.Вставить("ИД", СтрукутраРасшифровкаОтвета.ID);
		Ответ.Вставить("СтатусСообщения", Перечисления.СтатусыСообщений.Отправлено);
	КонецЕсли;
	
	Ответ.Вставить("Период", УниверсальноеВремя(ТекущаяДата()));
	
	Возврат Ответ;
	
КонецФункции

Функция ПроверитьСтатусСМС(Параметры, Ответ) Экспорт
	
	HTTPСоединение = Новый HTTPСоединение(Параметры.Сервер,Параметры.Порт,,,,Параметры.Таймаут,?(Параметры.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), Параметры.ИспользоватьАутентификациюОС);

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	ЗапросHTTP = Новый HTTPЗапрос("/modules/sms_status.php", Заголовки);
	СтрокаПараметров =  "username=" + Параметры.Пользователь + 
						"&password=" + Параметры.Пароль +
						"&id=" + Параметры.ИД;
	
	ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаПараметров,,ИспользованиеByteOrderMark.НеИспользовать);
	ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
	ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
	
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(ТелоОтвета);
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументHTML = ПостроительDOM.Прочитать(ЧтениеHTML);
	
	Узлы = ДокументHTML.ПолучитьЭлементыПоИмени("body")[0].ДочерниеУзлы;
	
	СтрукутраРасшифровкаОтвета = РасшифроватьУзлы(Узлы);
		
	Если СтрукутраРасшифровкаОтвета.Свойство("Error") Тогда 
		РасшифровкаОшибки = ПолучитьРасшифровкуОшибки(СтрукутраРасшифровкаОтвета.Error);
		Ответ.Вставить("Ошибка", РасшифровкаОшибки);
		Ответ.Вставить("СтатусСообщения", Перечисления.СтатусыСообщений.НеДоставлено);
	ИначеЕсли  СтрукутраРасшифровкаОтвета.Свойство("Status") Тогда
		Статус = ПолучитьСтатусСообщения(СтрукутраРасшифровкаОтвета.Status);
		Ответ.Вставить("СтатусСообщения", Статус);
	КонецЕсли;
	
	Ответ.Вставить("Период", УниверсальноеВремя(ТекущаяДата()));
	
	Возврат Ответ;

КонецФункции

Функция ПолучитьРасшифровкуОшибки(ТекстОшибки)
	
	РасшифрокаОшибки = "";
	Если ТекстОшибки = "Invalid request" Тогда 
		РасшифрокаОшибки = "проверьте наличие всех неоходимых параметров в запросе";
	ИначеЕсли ТекстОшибки = "Invalid username username or password password or user is blocked" Тогда 
		РасшифрокаОшибки = "Проверьте логин и пароль и то, что ваш аккаунт не заблокирован";
	ИначеЕсли ТекстОшибки = "Invalid or missing 'from' address" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и формат номера получателя";
	ИначеЕсли ТекстОшибки = "Invalid or missing 'to' address" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и длину адреса отправителя";
	ИначеЕсли ТекстОшибки = "Invalid or missing coding" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и значение параметра coding";
	ИначеЕсли ТекстОшибки = "Missing text" Тогда 
		РасшифрокаОшибки = "Проверьте наличие параметра text";
	ИначеЕсли ТекстОшибки = "Text too long" Тогда 
		РасшифрокаОшибки = "Проверьте длину параметра text";
	ИначеЕсли ТекстОшибки = "Invalid or missing mclass" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и значение параметра mclass";
	ИначеЕсли ТекстОшибки = "Invalid or missing priority" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и значение параметра priority";
	ИначеЕсли ТекстОшибки = "Invalid or missing dlrmask" Тогда 
		РасшифрокаОшибки = "Проверьте наличие и значение параметра dlrmask";
	ИначеЕсли ТекстОшибки = "IP not allowed" Тогда 
		РасшифрокаОшибки = "Ваш IP заблокирован";
	ИначеЕсли ТекстОшибки = "Max limit exceeded" Тогда 
		РасшифрокаОшибки = "Вы достили максимального количества СМС";
	ИначеЕсли ТекстОшибки = "Insufficient balance" Тогда 
		РасшифрокаОшибки = "У вас недостаточно средств на балансе";
	ИначеЕсли ТекстОшибки = "Invalid or missing missing message message ID" Тогда 
		РасшифрокаОшибки = "Проверьте наличие идентификатора сообщения";
	ИначеЕсли ТекстОшибки = "Unknown message ID" Тогда 
		РасшифрокаОшибки = "Проверьте идентификатор сообщения";
	КонецЕсли;
	
	Возврат РасшифрокаОшибки;
	
КонецФункции

Функция ПолучитьСтатусСообщения(КодСтатуса)
	
	Если КодСтатуса = "16" Тогда
		Возврат Перечисления.СтатусыСообщений.НеОтправлено;
	ИначеЕсли КодСтатуса = "32" Тогда
		Возврат Перечисления.СтатусыСообщений.НеОтправлено;
	ИначеЕсли КодСтатуса = "2" Тогда
		Возврат Перечисления.СтатусыСообщений.НеДоставлено;
	ИначеЕсли КодСтатуса = "0" Тогда
		Возврат Перечисления.СтатусыСообщений.Отправлено;
	ИначеЕсли КодСтатуса = "4" Тогда
		Возврат Перечисления.СтатусыСообщений.Отправлено;
	ИначеЕсли КодСтатуса = "8" Тогда
		Возврат Перечисления.СтатусыСообщений.Отправлено;
	ИначеЕсли КодСтатуса = "1" Тогда
		Возврат Перечисления.СтатусыСообщений.Доставлено;
	Иначе
		Возврат КодСтатуса;
	КонецЕсли;
	
КонецФункции

Функция РасшифроватьУзлы(Узлы)
	
	СтрукутраРасшифровки = Новый Структура;
	
	Для Каждого Узел Из Узлы Цикл
		Если Узел.ТипУзла = ТипУзлаDOM.Текст Тогда 
			Если СтрНайти(Узел.ТекстовоеСодержимое, "ID:") > 0 Тогда
				СтрукутраРасшифровки.Вставить("ID",СокрЛП(СтрЗаменить(Узел.ТекстовоеСодержимое,"ID:","")));
			ИначеЕсли СтрНайти(Узел.ТекстовоеСодержимое, "Status:") > 0 Тогда
				СтрукутраРасшифровки.Вставить("Status",СокрЛП(СтрЗаменить(Узел.ТекстовоеСодержимое,"Status:","")));
			ИначеЕсли СтрНайти(Узел.ТекстовоеСодержимое, "Status update time:") > 0 Тогда
				СтрукутраРасшифровки.Вставить("StatusUpdateTime",СокрЛП(СтрЗаменить(Узел.ТекстовоеСодержимое,"Status update time:","")));
			ИначеЕсли СтрНайти(Узел.ТекстовоеСодержимое, "Error:") > 0 Тогда
				СтрукутраРасшифровки.Вставить("Error",СокрЛП(СтрЗаменить(Узел.ТекстовоеСодержимое,"Error:","")));
			ИначеЕсли СтрНайти(Узел.ТекстовоеСодержимое, "Success") > 0 Тогда
				СтрукутраРасшифровки.Вставить("Success",СокрЛП(СтрЗаменить(Узел.ТекстовоеСодержимое,"Success:","")));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат  СтрукутраРасшифровки;
	
КонецФункции


