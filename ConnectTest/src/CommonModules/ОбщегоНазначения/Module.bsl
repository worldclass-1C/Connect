Function ProcessRequest(request) Export

	dateInMilliseconds = CurrentUniversalDateInMilliseconds();

	parameters = New Structure();
	parameters.Insert("url", request.BaseURL + request.RelativeURL);
	parameters.Insert("headers", HTTP.getJSONFromStructure(request.Headers));
	parameters.Insert("requestName", HTTP.getRequestHeader(request, "request"));
	parameters.Insert("language", HTTP.getRequestHeader(request, "language"));
	parameters.Insert("brand", HTTP.getRequestHeader(request, "brand"));
	parameters.Insert("notSaveAnswer", False);
	parameters.Insert("compressAnswer", False);

	If parameters.requestName = Undefined Or parameters.requestName = "" Then
		parameters.Insert("errorDescription", Service.getErrorDescription(parameters.language, "noRequest"));
	Else

		If parameters.requestName <> "kpolist" And parameters.requestName <> "auth"
				And parameters.requestName <> "phonemasklist"
				And parameters.requestName <> "restore" Then
			parameters.Insert("token", HTTP.GetRequestHeader(request, "auth-key"));
			checkResult = ОбщегоНазначенияПовторноеИспользование.checkToken(parameters.language, parameters.token);
			parameters.Insert("checkResult", checkResult);
			parameters.Insert("token", checkResult.token);
			parameters.Insert("errorDescription", checkResult.errorDescription);
		Else
			parameters.Insert("token", Catalogs.Токены.EmptyRef());
			parameters.Insert("errorDescription", Service.getErrorDescription());
		EndIf;

		requestParameters = HTTP.GetStructureFromRequest(request);
		parameters.Insert("requestStruct", requestParameters.requestStruct);
		parameters.Insert("requestBody", requestParameters.requestBody);
		parameters.Insert("answerBody", "");

		If parameters.errorDescription.error = "" Then
			Try
				If parameters.requestName = "kpolist" Then
					getChainList(parameters);
				ElsIf parameters.requestName = "phonemasklist" Then
					getPhoneMaskList(parameters);
				ElsIf parameters.requestName = "auth" Then
					userAuthorization(parameters);
				ElsIf parameters.requestName = "restore" Then
					ВосстановитьПарольПользователя(parameters);
				ElsIf parameters.requestName = "newpassword" Then
					УстановитьПарольПользователя(parameters);
				ElsIf parameters.requestName = "registerdevice" Then
					ЗарегистрироватьУстройство(parameters);
				ElsIf parameters.requestName = "unregisterdevice" Then
					УдалитьУстройство(parameters);
				ElsIf parameters.requestName = "userprofile" Then
					ПолучитьПрофильПользователя(parameters);
				ElsIf parameters.requestName = "cataloggyms"
						Or parameters.requestName = "gymlist" Then
					ПолучитьСписокКлубов(parameters);
				ElsIf parameters.requestName = "catalogcancelcauses" Then
					ПолучитьСписокПричинОтменыЗаписи(parameters);
				ElsIf parameters.requestName = "notificationlist" Then
					ПолучитьСписокСообщений(parameters);
				ElsIf parameters.requestName = "readnotification" Then
					ПрочитатьСообщение(parameters);
				ElsIf parameters.requestName = "unreadnotificationcount" Then
					КоличествоНеПрочитанныхСообщений(parameters);
				Else
					ВыполнитьВнешнийЗапрос(parameters);
				EndIf;
			Except
				parameters.Insert("errorDescription", Service.getErrorDescription(parameters.language, "system", ErrorDescription()));
			EndTry;
		EndIf;
	EndIf;

	If parameters.errorDescription.error <> "" Then
		JSONWriter = New JSONWriter();
		JSONWriter.SetString();
		answerStruct = New Structure();
		answerStruct.Insert("result", parameters.errorDescription.error);
		answerStruct.Insert("description", parameters.errorDescription.description);
		WriteJSON(JSONWriter, answerStruct);
		parameters.Insert("answerBody", JSONWriter.Close());
		If parameters.errorDescription.error = "userNotIdentified" Then
			answer = New HTTPServiceResponse(401);
		Else
			answer = New HTTPServiceResponse(403);
		EndIf;
	Else
		answer = New HTTPServiceResponse(200);
	EndIf;

	answer.Headers.Insert("Content-type", "application/json;  charset=utf-8");
	answer.SetBodyFromString(parameters.answerBody, TextEncoding.UTF8, ByteOrderMarkUsage.Use);

	parameters.Insert("duration", CurrentUniversalDateInMilliseconds()
		- DateInMilliseconds);
	parameters.Insert("isError", parameters.errorDescription.error <> "");

	Service.logRequestBackground(parameters);

	Return answer;

EndFunction

Функция ИзмененитьДанные(Запрос) Экспорт

	ИмяЗапроса = НРег(HTTP.GetRequestHeader(Запрос, "request"));
	ЯзыкПриложения = НРег(HTTP.GetRequestHeader(Запрос, "language"));
	ОписаниеОшибки = Service.getErrorDescription();

	Если ИмяЗапроса = Неопределено Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "noRequest");
	Иначе
		JSONСтрока = ИзменитьСоздатьЭлементыСправочника(ИмяЗапроса, Запрос, ЯзыкПриложения);
	КонецЕсли;

	Если ОписаниеОшибки.Служебное <> "" Тогда
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		СтруктураJSON = Новый Структура;
		СтруктураJSON.Вставить("result", ОписаниеОшибки.Служебное);
		СтруктураJSON.Вставить("description", ОписаниеОшибки.Пользовательское);
		ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
		JSONСтрока = ЗаписьJSON.Закрыть();
	КонецЕсли;

	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-type", "application/json;  charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(JSONСтрока, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.Использовать);

	Возврат Ответ;

КонецФункции

Функция ОтправитьСообщение(Запрос) Экспорт

	ЯзыкПриложения = НРег(HTTP.GetRequestHeader(Запрос, "language"));
	ОписаниеОшибки = Service.getErrorDescription();
	ТокенХолдинга = HTTP.GetRequestHeader(Запрос, "auth-key");

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	Если ТокенХолдинга = Неопределено Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
	Иначе
		Холдинг = Справочники.Холдинги.НайтиПоРеквизиту("Токен", ТокенХолдинга);
		Если Холдинг.Пустая() Тогда
			ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
		КонецЕсли;
	КонецЕсли;

	Если ОписаниеОшибки.Служебное = "" Тогда
		ДанныеЗапроса = HTTP.GetStructureFromRequest(Запрос).RequestStruct;
		Если ОписаниеОшибки.Служебное = "" Тогда
			Если Не ДанныеЗапроса.Свойство("messages") Тогда
				ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "noMessages");
			Иначе
				Для Каждого Сообщение Из ДанныеЗапроса.messages Цикл
					ДанныеСообщения = Новый Структура;
					ДанныеСообщения.Вставить("ОбъектИД", ?(Сообщение.Свойство("objectId"), Сообщение.objectId, ""));
					ДанныеСообщения.Вставить("ОбъектТип", ?(Сообщение.Свойство("objectType"), Сообщение.objectType, ""));
					ДанныеСообщения.Вставить("НомерТелефона", ?(Сообщение.Свойство("phone"), Сообщение.phone, ""));
					ДанныеСообщения.Вставить("Заголовок", ?(Сообщение.Свойство("title"), Сообщение.title, ?(ЯзыкПриложения = "ru", "Уведомление", "Notification")));
					ДанныеСообщения.Вставить("Текст", ?(Сообщение.Свойство("text"), Сообщение.text, ""));
					ДанныеСообщения.Вставить("Действие", ?(Сообщение.Свойство("action"), Сообщение.action, "ViewNotification"));
					ДанныеСообщения.Вставить("Приоритет", ?(Сообщение.Свойство("priority"), Сообщение.priority, 5));
					ДанныеСообщения.Вставить("Холдинг", Холдинг);
					Если Сообщение.Свойство("gymId") И Сообщение.gymId <> "" Тогда
						ДанныеСообщения.Вставить("Клуб", XMLЗначение(Тип("СправочникСсылка.Клубы"), Сообщение.gymId));
					Иначе
						ДанныеСообщения.Вставить("Клуб", Справочники.Клубы.ПустаяСсылка());
					КонецЕсли;
					Если Сообщение.Свойство("uid") И Сообщение.uid <> "" Тогда
						ДанныеСообщения.Вставить("Пользователь", XMLЗначение(Тип("СправочникСсылка.Пользователи"), Сообщение.uid));
					Иначе
						ДанныеСообщения.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
					КонецЕсли;
					Если Сообщение.Свойство("token") И Сообщение.token <> "" Тогда
						ДанныеСообщения.Вставить("Токен", XMLЗначение(Тип("СправочникСсылка.Токены"), Сообщение.token));
					Иначе
						ДанныеСообщения.Вставить("Токен", Справочники.Токены.ПустаяСсылка());
					КонецЕсли;
					МассивКаналов = Новый Массив;
					Если Сообщение.Свойство("routes") Тогда
						Для Каждого Канал Из Сообщение.routes Цикл
							МассивКаналов.Добавить(Перечисления.КаналыИнформирования[Канал]);
						КонецЦикла;
					КонецЕсли;
					ДанныеСообщения.Вставить("КаналыИнформирования", МассивКаналов);
					Если ДанныеСообщения.НомерТелефона = ""
							И ДанныеСообщения.Пользователь.ПолучитьОбъект() = Неопределено Тогда
					ИначеЕсли ДанныеСообщения.Пользователь = ДанныеСообщения.Токен.Пользователь Тогда
					Иначе
						РаботаССообщениями.НовоеСообщение(ДанныеСообщения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		СтруктураJSON.Вставить("result", "Ok");
	КонецЕсли;

	Если ОписаниеОшибки.Служебное <> "" Тогда
		СтруктураJSON.Вставить("result", ОписаниеОшибки.Служебное);
		СтруктураJSON.Вставить("description", ОписаниеОшибки.Пользовательское);
	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
	ОтветJSON = ЗаписьJSON.Закрыть();

	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-type", "application/json;  charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ОтветJSON, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.Использовать);

	Возврат Ответ;

КонецФункции

//------------------------------------------------------------
//Обработчики внешних запросов
Procedure getChainList(parameters)

	language = parameters.language;
	brand = parameters.brand;
	array = New array;

	query = New Query();
	query.Текст = "ВЫБРАТЬ
		|	Сети.Наименование КАК Наименование,
		|	Сети.НаименованиеRu КАК НаименованиеRu,
		|	Сети.code КАК code,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Сети.ПрограммаЛояльности) КАК bonusSystem,
		|	Сети.МаскаНомераТелефона КАК МаскаНомераТелефона,
		|	Сети.Валюта КАК currencySymbol,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Сети.Бренд) КАК brand,
		|	Сети.МаскаНомераТелефона.КодСтраны КАК МаскаНомераТелефонаКодСтраны,
		|	Сети.МаскаНомераТелефона.Наименование КАК МаскаНомераТелефонаНаименование
		|ИЗ
		|	Справочник.Сети КАК Сети
		|ГДЕ
		|	НЕ Сети.ПометкаУдаления
		|	И Сети.Бренд = &brand
		|УПОРЯДОЧИТЬ ПО
		|	Код";

	If brand = "" Then
		query.Text = StrReplace(query.Text, "И Сети.Бренд = &brand", "");
		nameTogetherChain = True;
	Else
		query.SetParameter("brand", Enums.ВидыБрендов[brand]);
		nameTogetherChain = False;
	EndIf;

	selection = query.Execute().Select();

	While selection.Next() Do
		chainStruct = New Structure();
		chainStruct.Insert("brand", selection.brand);
		chainStruct.Insert("code", selection.code);
		chainStruct.Insert("bonusSystem", selection.bonusSystem);
		chainStruct.Insert("currencySymbol", selection.currencySymbol);
		chainName = ?(language = "ru", selection.НаименованиеRu, selection.Наименование);
		chainName = ?(nameTogetherChain, selection.brand + " "
			+ chainName, chainName);
		chainStruct.Вставить("name", chainName);
		chainStruct.Вставить("phoneMask", New Structure("code, mask", selection.МаскаНомераТелефонаКодСтраны, selection.МаскаНомераТелефонаНаименование));

		array.add(chainStruct);
	EndDo;

	JSONWriter = New JSONWriter();
	JSONWriter.SetString();
	WriteJSON(JSONWriter, array);

	Parameters.Insert("answerBody", JSONWriter.Close());
	Parameters.Insert("notSaveAnswer", True);

EndProcedure

Procedure getPhoneMaskList(Parameters)
	JSONWriter = New JSONWriter();
	JSONWriter.SetString();
	WriteJSON(JSONWriter, ОбщегоНазначенияПовторноеИспользование.phoneMasksList());
	Parameters.Insert("answerBody", JSONWriter.Close());
	Parameters.Insert("notSaveAnswer", True);
EndProcedure

Procedure userAuthorization(Parameters)

	requestStruct	= Parameters.requestStruct;
	language		= Parameters.language;
	struct			= New Structure();
	currentDate		= ToUniversalTime(CurrentDate());
	errorDescription = Service.getErrorDescription();

	If Not requestStruct.Property("login") Then
		errorDescription = Service.getErrorDescription(language, "noUserLogin");
	ElsIf Not requestStruct.Property("password")
			Or requestStruct.password = "" Then
		errorDescription = Service.getErrorDescription(language, "noUserPassword");
	ElsIf Not requestStruct.Property("kpoCode") Then
		errorDescription = Service.getErrorDescription(language, "noKpoCode");
	EndIf;

	If errorDescription.error = "" Then
		query = New Query();
		query.Text = "ВЫБРАТЬ
		|	Пользователи.Ref КАК ref,
		|	Пользователи.Холдинг КАК holding,
		|	Пользователи.ТипПользователя КАК userType,
		|	UserPasswords.Validity КАК validity,
		|	Сети.ЧасовойПояс КАК timezone,
		|	Сети.Ссылка КАК chain
		|FROM
		|	Справочник.Пользователи КАК Пользователи
		|		LEFT JOIN InformationRegister.UserPasswords AS UserPasswords
		|		ON UserPasswords.user = Пользователи.Ref
		|		LEFT СОЕДИНЕНИЕ Справочник.Сети КАК Сети
		|		ПО Пользователи.Холдинг = Сети.Холдинг
		|ГДЕ
		|	Пользователи.Логин = &login
		|	AND UserPasswords.password = &password
		|	AND Сети.Code = &chainCode";

		query.SetParameter("login", requestStruct.login);
		query.SetParameter("password", requestStruct.password);
		query.SetParameter("chainCode", requestStruct.kpoCode);		

		queryResult = query.Execute();
		If queryResult.IsEmpty() Then
			errorDescription = Service.getErrorDescription(language, "PasswordIsNotCorrect");
		Else
			selection = queryResult.Select();		
			selection.Next();
			If Lower(selection.userType) <> "employee"
					And Lower(requestStruct.appType) = "employee" Then
				errorDescription = Service.getErrorDescription(language, "staffOnly");
			ElsIf selection.validity = Date(1, 1, 1)
					Or selection.validity >= currentDate Then
				struct.Insert("result", ?(selection.validity = Date(1, 1, 1), "Ok", "PasswordHasExpirationDate"));
				If requestStruct.Property("remember")
						And requestStruct.remember = True Then
					tokenObject = Users.token(requestStruct, selection.user, selection.chain, selection.holding, selection.timezone);
					struct.Insert("authToken", New Structure("key,createTime", XMLString(tokenObject.Ref), tokenObject.ДатаСоздания));
					parameters.Insert("token", tokenObject.Ref);
				EndIf;
			Else
				errorDescription = Service.getErrorDescription(language, "userPasswordExpired");
			EndIf;
		EndIf;

	EndIf;
	
	JSONWriter = New JSONWriter();;
	JSONWriter.SetString();
	WriteJSON(JSONWriter, Struct);		
	Parameters.Insert("answerBody", JSONWriter.Close());
	Parameters.Insert("errorDescription", errorDescription);

EndProcedure

Procedure ВосстановитьПарольПользователя(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	ЯзыкПриложения = Parameters.ЯзыкПриложения;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;
	ОписаниеОшибки = Service.getErrorDescription();

	МассивПользователей = Новый Массив;

	Если Не ДанныеЗапроса.Свойство("phone") Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "noUserPhone");
	ИначеЕсли Не ДанныеЗапроса.Свойство("kpoCode") Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "noKpoCode");
	КонецЕсли;

	Если ОписаниеОшибки.Служебное = "" Тогда
		ОписаниеОшибки = Service.canSendSms(ЯзыкПриложения, ДанныеЗапроса.phone);
	КонецЕсли;

	Если ОписаниеОшибки.Служебное = "" Тогда
		ТекстыЗапроса = Новый Массив;
		пЗапрос = Новый Запрос;
		ТекстЗапроса = "ВЫБРАТЬ
			|	Пользователи.Ссылка КАК Ссылка,
			|	Сети.Ссылка КАК Сеть,
			|	Пользователи.Холдинг КАК Холдинг,
			|	Пользователи.КодПользователя КАК КодПользователя
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сети КАК Сети
			|		ПО Пользователи.Холдинг = Сети.Холдинг
			|ГДЕ
			|	Сети.Код = &КодСети";

		Если ДанныеЗапроса.Свойство("uid") И ДанныеЗапроса.uid <> "" Тогда
			ДопУсловие = "И Пользователи.Ссылка = &Пользователь";
			пЗапрос.УстановитьПараметр("Пользователь", Справочники.Пользователи.ПолучитьСсылку(Новый УникальныйИдентификатор(ДанныеЗапроса.uid)));
		Иначе
			ДопУсловие = "И Пользователи.НомерТелефона = &НомерТелефона";
			пЗапрос.УстановитьПараметр("НомерТелефона", ДанныеЗапроса.phone);
		КонецЕсли;

		пЗапрос.УстановитьПараметр("КодСети", ДанныеЗапроса.kpoCode);
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		ТекстыЗапроса.Добавить(ДопУсловие);
		пЗапрос.Текст = СтрСоединить(ТекстыЗапроса, Символы.ПС);

		РезультатЗапроса = пЗапрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Холдинг = Справочники.Сети.НайтиПоКоду(ДанныеЗапроса.kpoCode).Холдинг;
			СтруктураЗапроса = HTTP.GetRequestStructure("userProfile", Холдинг);
			Если СтруктураЗапроса.Количество() > 0 Тогда
				Заголовки = Новый Соответствие;
				Заголовки.Вставить("Content-Type", "application/json");
				HTTPСоединение = Новый HTTPСоединение(СтруктураЗапроса.Сервер, , СтруктураЗапроса.УчетнаяЗапись, СтруктураЗапроса.Пароль, , СтруктураЗапроса.Таймаут, ?(СтруктураЗапроса.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), СтруктураЗапроса.ИспользоватьАутентификациюОС);
				ЗапросHTTP = Новый HTTPЗапрос(СтруктураЗапроса.URL
					+ СтруктураЗапроса.Приемник, Заголовки);
				ЗапросHTTP.УстановитьТелоИзСтроки(Parameters.ТелоЗапроса);
				ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
				СтруктураАтрибутов = Service.СтруктураАтрибутовВнешнегоЗапроса("users");
				МассивПользователей = СоздатьЭлементыСправочника(СтруктураАтрибутов, HTTP.GetStructureFromRequest(ОтветHTTP).RequestStruct, Холдинг);
			Иначе
				ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
			КонецЕсли;
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				МассивПользователей.Добавить(Выборка.Ссылка);
			КонецЦикла;
		КонецЕсли;

		Если МассивПользователей.Количество() = 0 Тогда
			ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
		ИначеЕсли МассивПользователей.Количество() > 1 Тогда
			МассивJSON = Новый Массив;
			Для Каждого Пользователь Из МассивПользователей Цикл
				СтруктураПользователя = Новый Структура;
				СтруктураПользователя.Вставить("uid", XMLСтрока(Пользователь));
				СтруктураПользователя.Вставить("name", Пользователь.Имя + " "
					+ Пользователь.Отчество + " " + Лев(Пользователь.Фамилия, 1));
				МассивJSON.Добавить(СтруктураПользователя);
			КонецЦикла;
			СтруктураJSON.Вставить("result", "Ok");
			СтруктураJSON.Вставить("users", МассивJSON);
		Иначе

			Пароль = Users.setUserPassword(МассивПользователей[0]);
			МассивКаналов = Новый Массив;
			МассивКаналов.Добавить(Перечисления.КаналыИнформирования.sms);

			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(?(ЯзыкПриложения = "ru", "Логин: ", "Login: "));
			МассивСтрок.Добавить(МассивПользователей[0].КодПользователя);
			МассивСтрок.Добавить(?(ЯзыкПриложения = "ru", " пароль: ", " password: "));
			МассивСтрок.Добавить(Пароль);
			МассивСтрок.Добавить(?(ЯзыкПриложения = "ru", ", пароль действителен в течение 15 минут", ", password is valid for 15 minutes"));

			ДанныеСообщения = Новый Структура;
			ДанныеСообщения.Вставить("НомерТелефона", ДанныеЗапроса.phone);
			ДанныеСообщения.Вставить("Пользователь", МассивПользователей[0]);
			ДанныеСообщения.Вставить("Заголовок", "Восстановить доступ");
			ДанныеСообщения.Вставить("Текст", СтрСоединить(МассивСтрок));
			ДанныеСообщения.Вставить("Холдинг", МассивПользователей[0].Холдинг);
			ДанныеСообщения.Вставить("КаналыИнформирования", МассивКаналов);
			ДанныеСообщения.Вставить("Приоритет", 0);
			РаботаССообщениями.НовоеСообщение(ДанныеСообщения);
			СтруктураJSON.Вставить("result", "Ok");
			Service.FixSandingMessage(ДанныеЗапроса.phone);
		КонецЕсли;

	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());
	Parameters.Insert("ОписаниеОшибки", ОписаниеОшибки);

EndProcedure

Procedure УстановитьПарольПользователя(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	ОписаниеОшибки = Users.checkPassword(ЯзыкПриложения, РезультатПроверки.Пользователь, ДанныеЗапроса.Password);
	Если ДанныеЗапроса.newPassword = "" Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "passwordIsEmpty");
	КонецЕсли;
	Если ОписаниеОшибки.Служебное = "" Тогда
		record = InformationRegisters.UserPasswords.CreateRecordManager();
		record.User 	= РезультатПроверки.Пользователь;
		record.Password = ДанныеЗапроса.newPassword;
		record.Validity = Дата(1, 1, 1);
		record.Write();
		СтруктураJSON.Вставить("result", "Ok");
	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());
	Parameters.Insert("ОписаниеОшибки", ОписаниеОшибки);

EndProcedure

Procedure ЗарегистрироватьУстройство(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	Если Не ДанныеЗапроса.Свойство("deviceToken") Тогда
		Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "noDeviceToken"));
	ИначеЕсли Не ДанныеЗапроса.Свойство("systemVersion") Тогда
		Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "noSystemVersion"));
	ИначеЕсли Не ДанныеЗапроса.Свойство("appVersion") Тогда
		Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "noAppVersion"));
	КонецЕсли;

	Если Parameters.ОписаниеОшибки.Служебное = "" Тогда
		СтруктураJSON.Вставить("result", "Ok");
		Запись = РегистрыСведений.ЗарегистрированныеУстройства.СоздатьМенеджерЗаписи();
		Запись.Токен = РезультатПроверки.Токен;
		Запись.ТокенУстройства = ДанныеЗапроса.deviceToken;
		Запись.ВерсияОС = ДанныеЗапроса.systemVersion;
		//Попытка
		//	Запись.ВерсияПриложения		= ДанныеЗапроса.appVersion;
		//Исключение
		Запись.ВерсияПриложения = Число(СтрЗаменить(ДанныеЗапроса.appVersion, ".", ""));
		//КонецПопытки;
		Запись.МодельУстройства = ДанныеЗапроса.deviceModel;
		Запись.ДатаЗаписи = УниверсальноеВремя(ТекущаяДата());
		Запись.Записать();

		пЗапрос = Новый Запрос;
		пЗапрос.Текст = "ВЫБРАТЬ
			|	ЗарегистрированныеУстройства.Токен КАК Токен
			|ИЗ
			|	РегистрСведений.ЗарегистрированныеУстройства КАК ЗарегистрированныеУстройства
			|ГДЕ
			|	ЗарегистрированныеУстройства.Токен <> &Токен
			|	И ЗарегистрированныеУстройства.Токен.Пользователь = &Пользователь
			|	И ЗарегистрированныеУстройства.ВерсияОС = &ВерсияОС
			|	И ЗарегистрированныеУстройства.ВерсияПриложения = &ВерсияПриложения
			|	И ЗарегистрированныеУстройства.МодельУстройства = &МодельУстройства
			|	И ЗарегистрированныеУстройства.ТокенУстройства = &ТокенУстройства
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Справочник.Сообщения.ПустаяСсылка) КАК Сообщение,
			|	ВЫБОР
			|		КОГДА &ЯзыкПриложения = ""ru""
			|			ТОГДА ""Уведомление""
			|		ИНАЧЕ ""Notification""
			|	КОНЕЦ КАК Заголовок,
			|	ВЫБОР
			|		КОГДА &ЯзыкПриложения = ""ru""
			|			ТОГДА ""Обновите приложение""
			|		ИНАЧЕ ""Update the application""
			|	КОНЕЦ КАК Текст,
			|	""updateApp"" КАК Действие,
			|	"""" КАК ОбъектИД,
			|	"""" КАК ОбъектТип,
			|	ЗНАЧЕНИЕ(ПланОбмена.СообщенияКОтправке.ПустаяСсылка) КАК УзелСообщенияКОтправке,
			|	Токены.ОперационнаяСистема КАК ОперационнаяСистема,
			|	ВЫБОР
			|		КОГДА Токены.ОперационнаяСистема = ЗНАЧЕНИЕ(Перечисление.ОперационныеСистемы.Android)
			|			ТОГДА ""GCM""
			|		ИНАЧЕ ""APNS""
			|	КОНЕЦ КАК ТипПодписчика,
			|	&ТокенУстройства КАК ТокенУстройства,
			|	Токены.Ссылка КАК Токен,
			|	ВЫБОР
			|		КОГДА Токены.ВидПриложения = ЗНАЧЕНИЕ(Перечисление.ВидыПриложений.Customer)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КаналыИнформирования.pushCustomer)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КаналыИнформирования.pushEmployee)
			|	КОНЕЦ КАК КаналИнформирования,
			|	Токены.ДатаБлокировки КАК ДатаБлокировки,
			|	ЕСТЬNULL(СертификатПриложенияДляСети.Сертификат, СертификатПриложенияОбщий.Сертификат) КАК Сертификат
			|ИЗ
			|	Справочник.Токены КАК Токены
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыПриложений КАК СертификатПриложенияДляСети
			|		ПО Токены.Сеть = СертификатПриложенияДляСети.Сеть
			|			И Токены.ВидПриложения = СертификатПриложенияДляСети.ВидПриложения
			|			И Токены.ОперационнаяСистема = СертификатПриложенияДляСети.ОперационнаяСистема
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыПриложений КАК СертификатПриложенияОбщий
			|		ПО (СертификатПриложенияОбщий.Сеть = ЗНАЧЕНИЕ(Справочник.Сети.ПустаяСсылка))
			|			И Токены.ВидПриложения = СертификатПриложенияОбщий.ВидПриложения
			|			И Токены.ОперационнаяСистема = СертификатПриложенияОбщий.ОперационнаяСистема
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АктуальныеВерсииПриложений КАК АктуальныеВерсииПриложений
			|		ПО Токены.ВидПриложения = АктуальныеВерсииПриложений.ВидПриложения
			|			И Токены.ОперационнаяСистема = АктуальныеВерсииПриложений.ОперационнаяСистема
			|ГДЕ
			|	ЕСТЬNULL(АктуальныеВерсииПриложений.ВерсияПриложения, 0) > &ВерсияПриложения
			|	И Токены.Ссылка = &Токен";

		пЗапрос.УстановитьПараметр("Токен", РезультатПроверки.Токен);
		пЗапрос.УстановитьПараметр("ЯзыкПриложения", ЯзыкПриложения);
		пЗапрос.УстановитьПараметр("Пользователь", РезультатПроверки.Пользователь);
		пЗапрос.УстановитьПараметр("ВерсияОС", Запись.ВерсияОС);
		пЗапрос.УстановитьПараметр("ВерсияПриложения", Запись.ВерсияПриложения);
		пЗапрос.УстановитьПараметр("МодельУстройства", Запись.МодельУстройства);
		пЗапрос.УстановитьПараметр("ТокенУстройства", Запись.ТокенУстройства);

		РезультатыЗапроса = пЗапрос.ВыполнитьПакет();
		Выборка = РезультатыЗапроса[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			Users.blockToken(Выборка.Токен);
		КонецЦикла;

		Выборка = РезультатыЗапроса[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			РаботаССообщениями.ОтправитьPush(Выборка);
		КонецЦикла;

	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure УдалитьУстройство(Parameters)

//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	Users.blockToken(РезультатПроверки.Токен);
	СтруктураJSON.Вставить("result", "Ok");

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure ПолучитьПрофильПользователя(Parameters)

//@skip-warning
	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь,
		|	Пользователи.Логин КАК Логин,
		|	Пользователи.ДатаРождения КАК ДатаРождения,
		|	Пользователи.НомерТелефона КАК НомерТелефона,
		|	Пользователи.Email КАК Email,
		|	НЕ Пользователи.НеУчаствоватьВРассылкеEmail КАК УчаствоватьВРассылкеEmail,
		|	НЕ Пользователи.НеУчаствоватьВРассылкеСообщений КАК УчаствоватьВРассылкеСообщений,
		|	Пользователи.Пол КАК Пол,
		|	Не Пользователи.ЗапретитьОбновлятьПерсональныеДанные КАК РазрешитьОбновлятьПерсональныеДанные,
		|	Пользователи.Штрихкод КАК Штрихкод,
		|	Пользователи.КодПользователя КАК КодПользователя,
		|	Пользователи.Фамилия КАК Фамилия,
		|	Пользователи.Имя КАК Имя,
		|	Пользователи.Отчество КАК Отчество
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Пользователь
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостояниеПользователя.ТипЗначенияКэша.Наименование КАК ТипЗначенияКэша,
		|	СостояниеПользователя.Значение КАК ЗначениеКэша
		|ИЗ
		|	РегистрСведений.СостояниеПользователя КАК СостояниеПользователя
		|ГДЕ
		|	СостояниеПользователя.Пользователь = &Пользователь
		|	И СостояниеПользователя.ВидПриложения = &ВидПриложения";

	пЗапрос.УстановитьПараметр("Пользователь", РезультатПроверки.Пользователь);
	пЗапрос.УстановитьПараметр("ВидПриложения", РезультатПроверки.ВидПриложения);

	РезультатыЗапроса = пЗапрос.ВыполнитьПакет();
	РезультатЗапроса = РезультатыЗапроса[0];
	РезультатЗапроса1 = РезультатыЗапроса[1];

	Если РезультатЗапроса.Пустой() Тогда
		Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified"));
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СтруктураJSON.Вставить("login", Выборка.Логин);
		СтруктураJSON.Вставить("birthdayDate", Выборка.ДатаРождения);
		СтруктураJSON.Вставить("phoneNumber", Выборка.НомерТелефона);
		СтруктураJSON.Вставить("email", Выборка.Email);
		СтруктураJSON.Вставить("subscriptionEmail", Выборка.УчаствоватьВРассылкеEmail);
		СтруктураJSON.Вставить("subscriptionSms", Выборка.УчаствоватьВРассылкеСообщений);
		СтруктураJSON.Вставить("gender", Выборка.Пол);
		СтруктураJSON.Вставить("canUpdatePersonalData", Выборка.РазрешитьОбновлятьПерсональныеДанные);
		СтруктураJSON.Вставить("barcode", Выборка.Штрихкод);
		СтруктураJSON.Вставить("cid", Выборка.КодПользователя);
		СтруктураJSON.Вставить("uid", XMLСтрока(Выборка.Пользователь));
		СтруктураJSON.Вставить("lastName", Выборка.Фамилия);
		СтруктураJSON.Вставить("firstName", Выборка.Имя);
		СтруктураJSON.Вставить("secondName", Выборка.Отчество);

		Выборка = РезультатЗапроса1.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураJSON.Вставить(Выборка.ТипЗначенияКэша, HTTP.GetStructureFromRequestBody(Выборка.ЗначениеКэша));
		КонецЦикла;
	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure ПолучитьСписокКлубов(Parameters)

//@skip-warning
	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	МассивJSON = Новый Массив;
	СтруктураJSON = Новый Структура;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	Клубы.Наименование КАК Наименование,
		|	Клубы.Адрес КАК Адрес,
		|	Клубы.Ссылка КАК Клуб,
		|	Клубы.Город КАК Город,
		|	Клубы.Долгота КАК Долгота,
		|	Клубы.Сегмент КАК Сегмент,
		|	Клубы.Тип КАК Тип,
		|	Клубы.Широта КАК Широта,
		|	Клубы.Сеть КАК Сеть,
		|	Клубы.ГрафикРаботыОтделов.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Отдел КАК Отдел,
		|		Телефон КАК Телефон,
		|		ГрафикРаботыВБудни КАК ГрафикРаботыВБудни,
		|		ГрафикРаботыВВыходные КАК ГрафикРаботыВВыходные
		|	) КАК ГрафикРаботыОтделов,
		|	Клубы.Предопределенный КАК Предопределенный,
		|	Клубы.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.Клубы КАК Клубы
		|ГДЕ
		|	Клубы.Сеть = &Сеть
		|	И НЕ Клубы.ПометкаУдаления";

	пЗапрос.УстановитьПараметр("Сеть", РезультатПроверки.Сеть);
	Выборка = пЗапрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтруктураКлуба = Новый Структура;
		СтруктураКлуба.Вставить("gymId", XMLСтрока(Выборка.Клуб));
		СтруктураКлуба.Вставить("name", Выборка.Наименование);
		СтруктураКлуба.Вставить("type", Выборка.Тип);
		СтруктураКлуба.Вставить("cityId", XMLСтрока(Выборка.Город));
		СтруктураКлуба.Вставить("gymAddress", Выборка.Адрес);
		СтруктураКлуба.Вставить("divisionTitle", Выборка.Сегмент);

		КоординатыКлуба = Новый Структура;
		КоординатыКлуба.Вставить("latitude", Выборка.Широта);
		КоординатыКлуба.Вставить("longitude", Выборка.Долгота);
		СтруктураКлуба.Вставить("coords", КоординатыКлуба);

		МассивГрафик = Новый Массив;
		Для Каждого СтрокаГрафик Из Выборка.ГрафикРаботыОтделов.Выгрузить() Цикл
			График = Новый Структура;
			График.Вставить("name", СтрокаГрафик.Отдел);
			График.Вставить("phone", СтрокаГрафик.Телефон);
			График.Вставить("weekdaysTime", СтрокаГрафик.ГрафикРаботыВБудни);
			График.Вставить("holidaysTime", СтрокаГрафик.ГрафикРаботыВВыходные);
			МассивГрафик.Добавить(График);
		КонецЦикла;
		СтруктураКлуба.Вставить("departments", МассивГрафик);
		МассивJSON.Добавить(СтруктураКлуба);
	КонецЦикла;

	Если МассивJSON.Количество() = 0 Тогда
		ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
	Иначе
		ЗаписатьJSON(ЗаписьJSON, МассивJSON);
	КонецЕсли;

	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());
	Parameters.Insert("НеСохранятьОтветВЛогах", Истина);

EndProcedure

Procedure ПолучитьСписокПричинОтменыЗаписи(Parameters)

//@skip-warning
	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	МассивПричинОтмены = Новый Массив;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	ПричиныОтменыЗаписи.Ссылка КАК Ссылка,
		|	ПричиныОтменыЗаписи.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПричиныОтменыЗаписи КАК ПричиныОтменыЗаписи
		|ГДЕ
		|	ПричиныОтменыЗаписи.Холдинг = &Холдинг
		|	И НЕ ПричиныОтменыЗаписи.ПометкаУдаления";

	пЗапрос.УстановитьПараметр("Холдинг", РезультатПроверки.Холдинг);
	Выборка = пЗапрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураПричиныОтмены = Новый Структура("uid,name", XMLСтрока(Выборка.Ссылка), Выборка.Наименование);
		МассивПричинОтмены.Добавить(СтруктураПричиныОтмены);
	КонецЦикла;

	ЗаписатьJSON(ЗаписьJSON, МассивПричинОтмены);
	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());
	Parameters.Insert("НеСохранятьОтветВЛогах", Истина);

EndProcedure

Procedure ПолучитьСписокСообщений(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	МассивСообщений = Новый Массив;

	ДатаРегистрации = УниверсальноеВремя(?(ДанныеЗапроса.date = "", ТекущаяДата(), XMLЗначение(Тип("Дата"), ДанныеЗапроса.date)));

	Если РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Employee Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushEmployee;
	ИначеЕсли РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Customer Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushCustomer;
	Иначе
		КаналИнформирования = Перечисления.КаналыИнформирования.ПустаяСсылка();
	КонецЕсли;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 20
		|	Сообщения.Ссылка КАК Сообщение,
		|	Сообщения.ДатаРегистрации КАК ДатаРегистрации,
		|	Сообщения.Заголовок КАК Заголовок,
		|	Сообщения.Текст КАК Текст,
		|	Сообщения.ОбъектИД КАК ОбъектИД,
		|	Сообщения.ОбъектТип КАК ОбъектТип
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Справочник.Сообщения КАК Сообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сообщения.ПриоритетыКаналовИнформирования КАК СообщенияПриоритетыКаналовИнформирования
		|		ПО Сообщения.Ссылка = СообщенияПриоритетыКаналовИнформирования.Ссылка
		|ГДЕ
		|	СообщенияПриоритетыКаналовИнформирования.Канал = &КаналИнформирования
		|	И Сообщения.Пользователь = &Пользователь
		|	И Сообщения.ДатаРегистрации < &ДатаРегистрации
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРегистрации УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Сообщение КАК Сообщение,
		|	ВТ.ДатаРегистрации КАК ДатаРегистрации,
		|	ВТ.Заголовок КАК Заголовок,
		|	ВТ.Текст КАК Текст,
		|	ВТ.ОбъектИД КАК ОбъектИД,
		|	ВТ.ОбъектТип КАК ОбъектТип,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ИсторияСообщенийСрезПоследних.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.СтатусыСообщений.Прочитано)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК Прочитано
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСообщений.СрезПоследних КАК ИсторияСообщенийСрезПоследних
		|		ПО ВТ.Сообщение = ИсторияСообщенийСрезПоследних.Сообщение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Сообщение,
		|	ВТ.ДатаРегистрации,
		|	ВТ.Заголовок,
		|	ВТ.Текст,
		|	ВТ.ОбъектИД,
		|	ВТ.ОбъектТип
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРегистрации УБЫВ";

	пЗапрос.УстановитьПараметр("ДатаРегистрации", ДатаРегистрации);
	пЗапрос.УстановитьПараметр("Пользователь", РезультатПроверки.Пользователь);
	пЗапрос.УстановитьПараметр("КаналИнформирования", КаналИнформирования);

	Выборка = пЗапрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураСообщения = Новый Структура;
		СтруктураСообщения.Вставить("noteId", XMLСтрока(Выборка.Сообщение));
		СтруктураСообщения.Вставить("date", XMLСтрока(МестноеВремя(Выборка.ДатаРегистрации, РезультатПроверки.ЧасовойПояс)));
		СтруктураСообщения.Вставить("title", Выборка.Заголовок);
		СтруктураСообщения.Вставить("text", Выборка.Текст);
		СтруктураСообщения.Вставить("read", Выборка.Прочитано);
		СтруктураСообщения.Вставить("objectId", Выборка.ОбъектИД);
		СтруктураСообщения.Вставить("objectType", Выборка.ОбъектТип);
		МассивСообщений.Добавить(СтруктураСообщения);
	КонецЦикла;

	ЗаписатьJSON(ЗаписьJSON, МассивСообщений);
	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure ПрочитатьСообщение(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	Если Не ДанныеЗапроса.Свойство("noteId") Или ДанныеЗапроса.noteId = "" Тогда
		Сообщение = Справочники.Сообщения.ПустаяСсылка();
	Иначе
		Сообщение = XMLЗначение(Тип("СправочникСсылка.Сообщения"), ДанныеЗапроса.noteId);
	КонецЕсли;

	Если РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Employee Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushEmployee;
	ИначеЕсли РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Customer Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushCustomer;
	Иначе
		КаналИнформирования = Перечисления.КаналыИнформирования.ПустаяСсылка();
	КонецЕсли;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	Сообщения.Ссылка КАК Сообщение
		|ИЗ
		|	Справочник.Сообщения КАК Сообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сообщения.ПриоритетыКаналовИнформирования КАК СообщенияПриоритетыКаналовИнформирования
		|		ПО Сообщения.Ссылка = СообщенияПриоритетыКаналовИнформирования.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСообщений.СрезПоследних КАК ИсторияСообщенийСрезПоследних
		|		ПО Сообщения.Ссылка = ИсторияСообщенийСрезПоследних.Сообщение
		|ГДЕ
		|	СообщенияПриоритетыКаналовИнформирования.Канал = &КаналИнформирования
		|	И ЕСТЬNULL(ИсторияСообщенийСрезПоследних.СтатусСообщения, ЗНАЧЕНИЕ(Перечисление.СтатусыСообщений.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыСообщений.Прочитано)
		|	И Сообщения.Пользователь = &Пользователь";

	пЗапрос.УстановитьПараметр("Пользователь", РезультатПроверки.Пользователь);
	пЗапрос.УстановитьПараметр("КаналИнформирования", КаналИнформирования);

	РезультатЗапроса = пЗапрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		КоличествоНеПрочитанныхСообщений = Выборка.Количество();

		Если Сообщение.Пустая() Тогда
			Пока Выборка.Следующий() Цикл
				Запись = РегистрыСведений.ИсторияСообщений.СоздатьМенеджерЗаписи();
				Запись.Период = УниверсальноеВремя(ТекущаяДата());
				Запись.Сообщение = Выборка.Сообщение;
				Запись.Токен = РезультатПроверки.Токен;
				Запись.ДатаСобытия = Запись.Период;
				Запись.СтатусСообщения = Перечисления.СтатусыСообщений.Прочитано;
				Запись.КаналИнформирования = КаналИнформирования;
				Запись.Записать();
			КонецЦикла;
			КоличествоНеПрочитанныхСообщений = 0;
		Иначе
			Если Выборка.НайтиСледующий(Новый Структура("Сообщение", Сообщение)) Тогда
				Запись = РегистрыСведений.ИсторияСообщений.СоздатьМенеджерЗаписи();
				Запись.Период = УниверсальноеВремя(ТекущаяДата());
				Запись.Сообщение = Сообщение;
				Запись.Токен = РезультатПроверки.Токен;
				Запись.ДатаСобытия = Запись.Период;
				Запись.СтатусСообщения = Перечисления.СтатусыСообщений.Прочитано;
				Запись.КаналИнформирования = КаналИнформирования;
				Запись.Записать();
				КоличествоНеПрочитанныхСообщений = КоличествоНеПрочитанныхСообщений - 1;
			КонецЕсли;
		КонецЕсли;
	Иначе
		КоличествоНеПрочитанныхСообщений = 0;
	КонецЕсли;

	СтруктураJSON.Вставить("result", "Ok");
	СтруктураJSON.Вставить("quantity", КоличествоНеПрочитанныхСообщений);

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure КоличествоНеПрочитанныхСообщений(Parameters)

//@skip-warning
	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	//@skip-warning
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;

	Если РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Employee Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushEmployee;
	ИначеЕсли РезультатПроверки.ВидПриложения = Перечисления.ВидыПриложений.Customer Тогда
		КаналИнформирования = Перечисления.КаналыИнформирования.pushCustomer;
	Иначе
		КаналИнформирования = Перечисления.КаналыИнформирования.ПустаяСсылка();
	КонецЕсли;

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сообщения.Ссылка) КАК Количество
		|ИЗ
		|	Справочник.Сообщения КАК Сообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сообщения.ПриоритетыКаналовИнформирования КАК СообщенияПриоритетыКаналовИнформирования
		|		ПО Сообщения.Ссылка = СообщенияПриоритетыКаналовИнформирования.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСообщений.СрезПоследних КАК ИсторияСообщенийСрезПоследних
		|		ПО Сообщения.Ссылка = ИсторияСообщенийСрезПоследних.Сообщение
		|ГДЕ
		|	СообщенияПриоритетыКаналовИнформирования.Канал = &КаналИнформирования
		|	И ЕСТЬNULL(ИсторияСообщенийСрезПоследних.СтатусСообщения, ЗНАЧЕНИЕ(Перечисление.СтатусыСообщений.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыСообщений.Прочитано)
		|	И Сообщения.Пользователь = &Пользователь";

	пЗапрос.УстановитьПараметр("Пользователь", РезультатПроверки.Пользователь);
	пЗапрос.УстановитьПараметр("КаналИнформирования", КаналИнформирования);

	Выборка = пЗапрос.Выполнить().Выбрать();
	Выборка.Следующий();
	СтруктураJSON.Вставить("quantity", Выборка.Количество);

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
	Parameters.Insert("ТелоОтвета", ЗаписьJSON.Закрыть());

EndProcedure

Procedure ВыполнитьВнешнийЗапрос(Parameters)

	ДанныеЗапроса = Parameters.ДанныеЗапроса;
	ЯзыкПриложения = Parameters.ЯзыкПриложения;
	РезультатПроверки = Parameters.РезультатПроверки;
	ТелоОтвета = "";

	пЗапрос = Новый Запрос;
	пЗапрос.Текст = "ВЫБРАТЬ
		|	СоответствиеЗапросовИсточникамИнформации.ВыполнятьВФоне КАК ВыполнятьВФоне,
		|	СоответствиеЗапросовИсточникамИнформации.ЗапросПриемник КАК Приемник,
		|	СоответствиеЗапросовИсточникамИнформации.ТипЗапроса КАК ТипЗапроса,
		|	СоответствиеЗапросовИсточникамИнформации.Атрибут КАК Атрибут,
		|	СоответствиеЗапросовИсточникамИнформации.ТолькоДляСотрудников КАК ТолькоДляСотрудников,
		|	СоответствиеЗапросовИсточникамИнформации.НеСохранятьОтветВЛогах КАК НеСохранятьОтветВЛогах,
		|	СоответствиеЗапросовИсточникамИнформации.СжиматьЛоги КАК СжиматьЛоги,
		|	ПодключенияХолдинговКИсточникамИнформации.URL КАК URL,
		|	ПодключенияХолдинговКИсточникамИнформации.Сервер КАК Сервер,
		|	ПодключенияХолдинговКИсточникамИнформации.Порт КАК Порт,
		|	ПодключенияХолдинговКИсточникамИнформации.Пользователь КАК УчетнаяЗапись,
		|	ПодключенияХолдинговКИсточникамИнформации.Пароль КАК Пароль,
		|	ПодключенияХолдинговКИсточникамИнформации.Таймаут КАК Таймаут,
		|	ПодключенияХолдинговКИсточникамИнформации.ЗащищенноеСоединение КАК ЗащищенноеСоединение,
		|	ПодключенияХолдинговКИсточникамИнформации.ИспользоватьАутентификациюОС КАК ИспользоватьАутентификациюОС
		|ИЗ
		|	РегистрСведений.ПодключенияХолдинговКИсточникамИнформации КАК ПодключенияХолдинговКИсточникамИнформации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоответствиеЗапросовИсточникамИнформации.ИсточникиИнформации КАК СоответствиеЗапросовИсточникамИнформации
		|		ПО ПодключенияХолдинговКИсточникамИнформации.ИсточникИнформации = СоответствиеЗапросовИсточникамИнформации.ИсточникИнформации
		|			И (СоответствиеЗапросовИсточникамИнформации.ЗапросИсточник = &ИмяЗапроса)
		|			И (НЕ СоответствиеЗапросовИсточникамИнформации.НеИспользовать)
		|ГДЕ
		|	ПодключенияХолдинговКИсточникамИнформации.Холдинг = &Холдинг
		|	И НЕ СоответствиеЗапросовИсточникамИнформации.ЗапросПриемник ЕСТЬ NULL";

	пЗапрос.УстановитьПараметр("Холдинг", РезультатПроверки.Холдинг);
	пЗапрос.УстановитьПараметр("ИмяЗапроса", Parameters.requestName);
	РезультатЗапроса = пЗапрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "noUrl"));
	Иначе
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();

		Parameters.Insert("НеСохранятьОтветВЛогах", Выборка.НеСохранятьОтветВЛогах);
		Parameters.Insert("СжиматьЛоги", Выборка.СжиматьЛоги);

		Если Выборка.ТолькоДляСотрудников
				И РезультатПроверки.ТипПользователя <> "employee" Тогда
			Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "staffOnly"));
		Иначе

			ВыполнятьВФоне = Выборка.ВыполнятьВФоне;
			МассивФЗ = Новый Массив;
			КодСостояния = 200;
			Если Выборка.ТипЗапроса = Перечисления.ТипыHTTPЗапросов.GET Тогда
				ТелоЗапроса = "";
				ParametersИзURL = СтрЗаменить(Parameters.URL, ОбщегоНазначенияПовторноеИспользование.ПолучитьБазовыйURL(), "");
			Иначе
				ТелоЗапроса = HTTP.PrepareRequestBody(Parameters.КлючАвторизации, ДанныеЗапроса, РезультатПроверки.Пользователь, ЯзыкПриложения, РезультатПроверки.ЧасовойПояс, РезультатПроверки.ВидПриложения);
				ParametersИзURL = "";
			КонецЕсли;

			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл

				Если ВыполнятьВФоне Тогда
					Адрес = ПоместитьВоВременноеХранилище("");

					СтруктураПодключения = Новый Структура;
					СтруктураПодключения.Вставить("Сервер", Выборка.Сервер);
					СтруктураПодключения.Вставить("УчетнаяЗапись", Выборка.УчетнаяЗапись);
					СтруктураПодключения.Вставить("Пароль", Выборка.Пароль);
					СтруктураПодключения.Вставить("Таймаут", Выборка.Таймаут);
					СтруктураПодключения.Вставить("ЗащищенноеСоединение", Выборка.ЗащищенноеСоединение);
					СтруктураПодключения.Вставить("ИспользоватьАутентификациюОС", Выборка.ИспользоватьАутентификациюОС);
					СтруктураПодключения.Вставить("URL", Выборка.URL);
					СтруктураПодключения.Вставить("Приемник", Выборка.Приемник);
					ФЗ = Service.RunBackground(СтруктураПодключения, Заголовки, ТелоЗапроса, Адрес, ParametersИзURL);

					СтруктураФЗ = Новый Структура();
					СтруктураФЗ.Вставить("Адрес", Адрес);
					СтруктураФЗ.Вставить("ФЗ", ФЗ);
					СтруктураФЗ.Вставить("Атрибут", Выборка.Атрибут);
					МассивФЗ.Добавить(СтруктураФЗ);
				Иначе
					HTTPСоединение = Новый HTTPСоединение(Выборка.Сервер, , Выборка.УчетнаяЗапись, Выборка.Пароль, , Выборка.Таймаут, ?(Выборка.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), Выборка.ИспользоватьАутентификациюОС);
					ЗапросHTTP = Новый HTTPЗапрос(Выборка.URL + Выборка.Приемник
						+ ParametersИзURL, Заголовки);
					ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);

					Если Выборка.ТипЗапроса = Перечисления.ТипыHTTPЗапросов.GET Тогда
						ОтветHTTP = HTTPСоединение.Получить(ЗапросHTTP);
					Иначе
						ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
					КонецЕсли;
					КодСостояния = ОтветHTTP.КодСостояния;
					ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
				КонецЕсли;

			КонецЦикла;

			Если ВыполнятьВФоне Тогда
				ТелоОтвета = HTTP.CheckBackgroundJobs(МассивФЗ);
			КонецЕсли;

			Если КодСостояния <> 200 Тогда
				Если КодСостояния = 403 Тогда
					СтруктураHTTPЗапроса = HTTP.GetStructureFromRequestBody(ТелоОтвета);
					Если СтруктураHTTPЗапроса.Свойство("result") Тогда
						Parameters.Insert("ОписаниеОшибки", Новый Структура("Служебное, Пользовательское", СтруктураHTTPЗапроса.result, СтруктураHTTPЗапроса.description));
					КонецЕсли;
				Иначе
					Parameters.Insert("ОписаниеОшибки", Service.getErrorDescription(ЯзыкПриложения, "system", ТелоОтвета));
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	Parameters.Insert("ТелоОтвета", ТелоОтвета);

EndProcedure

//------------------------------------------------------------
//Обработчики внутренних запросов
Функция СоздатьЭлементыСправочника(СтруктураАтрибутов, ДанныеЗапроса, Холдинг)

	МассивЭлементов = Новый Массив;

	Если ТипЗнч(ДанныеЗапроса) = Тип("Массив") Тогда
		Для Каждого ПараметрЗапроса Из ДанныеЗапроса Цикл
			СправочникСсылка = Справочники[СтруктураАтрибутов.ИмяОбъектаМетаданных].ПолучитьСсылку(Новый УникальныйИдентификатор(ПараметрЗапроса.uid));
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
			УстановитьПароль = Ложь;
			Если СправочникОбъект = Неопределено Тогда
				СправочникОбъект = Справочники[СтруктураАтрибутов.ИмяОбъектаМетаданных].СоздатьЭлемент();
				СправочникОбъект.УстановитьСсылкуНового(СправочникСсылка);
				Для Каждого Атрибут Из СтруктураАтрибутов.ТаблицаАтрибутовДляНовогоЭлемента Цикл
					Если Атрибут.Ключ = "Пароль"
							И ПараметрЗапроса.Свойство(Атрибут.Значение) Тогда
						УстановитьПароль = Истина;
						Пароль = XMLЗначение(Тип(Атрибут.Тип), ПараметрЗапроса[Атрибут.Значение])
					Иначе
						СправочникОбъект[Атрибут.Ключ] = XMLЗначение(Тип(Атрибут.Тип), ПараметрЗапроса[Атрибут.Значение]);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Для Каждого Атрибут Из СтруктураАтрибутов.ТаблицаАтрибутов Цикл
				Если Атрибут.Тип = "ТабличнаяЧасть" Тогда
					СправочникОбъект[Атрибут.Ключ].Очистить();
					Для Каждого ЭлементМассива Из ПараметрЗапроса[Атрибут.Значение] Цикл
						НоваяСтрока = СправочникОбъект[Атрибут.Ключ].Добавить();
						Для Каждого РеквизитТЧ Из СтруктураАтрибутов.СтруктураМД[Атрибут.Ключ] Цикл
							Если РеквизитТЧ.Тип = "Ссылка" Тогда
								Для Каждого РеквизитСсылки Из СтруктураАтрибутов.СтруктураМД[РеквизитТЧ.Ключ] Цикл
									НоваяСтрока[РеквизитТЧ.Ключ] = Справочники[РеквизитСсылки.Ключ].ПолучитьСсылку(Новый УникальныйИдентификатор(ЭлементМассива[РеквизитТЧ.Значение][РеквизитСсылки.Значение]));
								КонецЦикла;
							Иначе
								НоваяСтрока[РеквизитТЧ.Ключ] = ЭлементМассива[РеквизитТЧ.Значение];
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				ИначеЕсли Атрибут.Тип = "Ссылка" Тогда
					Для Каждого РеквизитСсылки Из СтруктураАтрибутов.СтруктураМД[Атрибут.Ключ] Цикл
						СправочникОбъект[Атрибут.Ключ] = Справочники[РеквизитСсылки.Ключ].ПолучитьСсылку(Новый УникальныйИдентификатор(ПараметрЗапроса[Атрибут.Значение][РеквизитСсылки.Значение]));
					КонецЦикла;
				Иначе
					СправочникОбъект[Атрибут.Ключ] = XMLЗначение(Тип(Атрибут.Тип), ПараметрЗапроса[Атрибут.Значение]);
				КонецЕсли;
			КонецЦикла;
			Если СтруктураАтрибутов.ИмяОбъектаМетаданных <> "СоответствиеЗапросовИсточникамИнформации" Тогда
				СправочникОбъект.Холдинг = Холдинг;
				СправочникОбъект.ДатаРегистрации = УниверсальноеВремя(ТекущаяДата());
			КонецЕсли;
			СправочникОбъект.Записать();
			МассивЭлементов.Добавить(СправочникОбъект.Ссылка);
			Если УстановитьПароль Тогда
				Users.setUserPassword(СправочникОбъект.Ссылка, Пароль);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат МассивЭлементов;

КонецФункции

Функция ИзменитьСоздатьЭлементыСправочника(ИмяЗапроса, Запрос, ЯзыкПриложения)

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	СтруктураJSON = Новый Структура;
	ОписаниеОшибки = Service.getErrorDescription();

	СтруктураАтрибутов = Service.СтруктураАтрибутовВнешнегоЗапроса(ИмяЗапроса);

	Если СтруктураАтрибутов.ИмяОбъектаМетаданных = "" Тогда
		Ошибка = "no request name";
	КонецЕсли;

	ТокенХолдинга = HTTP.GetRequestHeader(Запрос, "auth-key");
	Если ТокенХолдинга = Неопределено Тогда
		ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
	Иначе
		Холдинг = Справочники.Холдинги.НайтиПоРеквизиту("Токен", ТокенХолдинга);
		Если Холдинг.Пустая() Тогда
			ОписаниеОшибки = Service.getErrorDescription(ЯзыкПриложения, "userNotIdentified");
		КонецЕсли;
	КонецЕсли;

	Если ОписаниеОшибки.Служебное = "" Тогда
		ДанныеЗапроса = HTTP.GetStructureFromRequest(Запрос).RequestStruct;
		СтруктураJSON.Вставить("result", "Ok");
		СоздатьЭлементыСправочника(СтруктураАтрибутов, ДанныеЗапроса, Холдинг);
	КонецЕсли;

	Если ОписаниеОшибки.Служебное <> "" Тогда
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		СтруктураJSON = Новый Структура;
		СтруктураJSON.Вставить("result", "error");
		СтруктураJSON.Вставить("description", Ошибка);
		ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);
	КонецЕсли;

	ЗаписатьJSON(ЗаписьJSON, СтруктураJSON);

	Возврат ЗаписьJSON.Закрыть();

КонецФункции

Procedure ОбновитьКэшПользователей(Parameters, Холдинг,
		МассивПользователей) Экспорт

	СтруктураЗапроса = HTTP.GetRequestStructure("userProfileCache", Холдинг);
	Если СтруктураЗапроса.Количество() > 0 Тогда
		Для Каждого Пользователь Из МассивПользователей Цикл
		КонецЦикла;		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		HTTPСоединение = Новый HTTPСоединение(СтруктураЗапроса.Сервер, , СтруктураЗапроса.УчетнаяЗапись, СтруктураЗапроса.Пароль, , СтруктураЗапроса.Таймаут, ?(СтруктураЗапроса.ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено), СтруктураЗапроса.ИспользоватьАутентификациюОС);
		ЗапросHTTP = Новый HTTPЗапрос(СтруктураЗапроса.URL
			+ СтруктураЗапроса.Приемник, Заголовки);
		ЗапросHTTP.УстановитьТелоИзСтроки(Parameters.ТелоЗапроса);
		ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
	КонецЕсли;
EndProcedure